/*

  Ski assignment in Comet.

  From  
  Jeffrey Lee Hellrung, Jr.: PIC 60, Fall 2008 â€“ Final Review, December 12, 2008
  http://www.math.ucla.edu/~jhellrun/course_files/Fall%25202008/PIC%252060%2520-%2520Data%2520Structures%2520and%2520Algorithms/final_review.pdf
  """
  5. Ski Optimization! Your job at Snapple is pleasant but in the winter you've 
  decided to become a ski bum. You've hooked up with the Mount Baldy Ski Resort. 
  They'll let you ski all winter for free in exchange for helping their ski rental 
  shop with an algorithm to assign skis to skiers. Ideally, each skier should 
  obtain a pair of skis whose height matches his or her own height exactly. 
  Unfortunately, this is generally not possible. We define the disparity between 
  a skier and his or her skis to be the absolute value of the difference between 
  the height of the skier and the pair of skis. Our objective is to find an 
  assignment of skis to skiers that minimizes the sum of the disparities. 
  ...
  Illustrate your algorithm by explicitly filling out the A[i, j] table for the 
  following sample data:
    * Ski heights: 1, 2, 5, 7, 13, 21.
    * Skier heights: 3, 4, 7, 11, 18.

  """


  This Comet model was created by Hakan Kjellerstrand (hakank@bonetmail.com)
  Also, see my Comet page: http://www.hakank.org/comet 

*/

import cotfd;

int t0 = System.getCPUTime();

int num_skis = 6;
int num_skiers = 5;
int ski_heights[1..num_skis] = [1, 2, 5, 7, 13, 21];
int skier_heights[1..num_skiers] = [3, 4, 7, 11, 18];

Solver<CP> m();
var<CP>{int} x[1..num_skiers](m, 1..num_skis);
expr<CP>{int} z = sum(i in 1..num_skiers) ( abs(ski_heights[x[i]] - skier_heights[i]) );

Integer num_solutions(0);
// explore<m> {
// exploreall<m> {
minimize<m> z subject to {

  // m.post(z == 7); // for exploreall
  m.post(alldifferent(x));

} using {
      

  label(m);
  num_solutions++;

  cout << "z: " << 1*z << endl;      
  cout << "x: " << x << endl;


}

cout << "\nnum_solutions: " << num_solutions << endl;
    
int t1 = System.getCPUTime();
cout << "time:      " << (t1-t0) << endl;
cout << "#choices = " << m.getNChoice() << endl;
cout << "#fail    = " << m.getNFail() << endl;
cout << "#propag  = " << m.getNPropag() << endl;

