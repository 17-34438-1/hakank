/*

  
  A Round of Golf puzzle (Dell Logic Puzzles) in Comet.
  
  From http://brownbuffalo.sourceforge.net/RoundOfGolfClues.html
  """
  Title: A Round of Golf
  Author: Ellen K. Rodehorst
  Publication: Dell Favorite Logic Problems
  Issue: Summer, 2000
  Puzzle #: 9
  Stars: 1
 
  When the Sunny Hills Country Club golf course isn't in use by club members, 
  of course, it's open to the club's employees. Recently, Jack and three other 
  workers at the golf course got together on their day off to play a round of 
  eighteen holes of golf. 
  Afterward, all four, including Mr. Green, went to the clubhouse to total 
  their scorecards. Each man works at a different job (one is a short-order 
  cook), and each shot a different score in the game. No one scored below 
  70 or above 85 strokes. From the clues below, can you discover each man's 
  full name, job and golf score?
  
  1. Bill, who is not the maintenance man, plays golf often and had the lowest 
  score of the foursome.
  2. Mr. Clubb, who isn't Paul, hit several balls into the woods and scored ten 
  strokes more than the pro-shop clerk.
  3. In some order, Frank and the caddy scored four and seven more strokes than 
  Mr. Sands.
  4. Mr. Carter thought his score of 78 was one of his better games, even 
     though Frank's score  was lower.
  5. None of the four scored exactly 81 strokes.
  
  Determine: First Name - Last Name - Job - Score 
  """

  Compare with the F1 model: 
  http://www.f1compiler.com/samples/A 20Round 20of 20Golf.f1.html

  Solution:
             Jack, Bill, Paul, Frank
             Clubb Sands Carter Green
             maint cook  caddy clerk
             85    71    78    75
  first_name: [1, 2, 3, 4]
  last_name : [4, 1, 2, 3]
  job       : [2, 1, 4, 3]
  score     : [85, 71, 78, 75]



  This Comet model was created by Hakan Kjellerstrand (hakank@bonetmail.com)
  Also, see my Comet page: http://www.hakank.org/comet 

*/

import cotfd;

int t0 = System.getCPUTime();

range R = 1..4;

int Jack = 1;
int Bill = 2;
int Paul = 3;
int Frank = 4;
int first_name[R] = [Jack, Bill, Paul, Frank];


Solver<CP> m();

var<CP>{int} Green(m, R);
var<CP>{int} Clubb(m, R);
var<CP>{int} Sands(m, R);
var<CP>{int} Carter(m, R);
var<CP>{int} last_name[R](m, R);
last_name[1] = Green; last_name[2] = Clubb;
last_name[3] = Sands; last_name[4] = Carter;

var<CP>{int} cook(m, R);
var<CP>{int} maintenance_man(m, R);
var<CP>{int} clerk(m, R);
var<CP>{int} caddy(m, R);
var<CP>{int} job[R](m, R);
job[1] = cook; job[2] = maintenance_man; 
job[3] = clerk; job[4] = caddy;

var<CP>{int} score[R](m, 70..85);

Integer num_solutions(0);

exploreall<m> {

  m.post(alldifferent(last_name));
  m.post(alldifferent(job));
  m.post(alldifferent(score));

  // 1. Bill, who is not the maintenance man, plays golf often and had 
  // the lowest score of the foursome.
  m.post(Bill != maintenance_man);
   
  m.post(score[Bill] < score[Jack]);
  m.post(score[Bill] < score[Paul]);
  m.post(score[Bill] < score[Frank]);
 
  // 2. Mr. Clubb, who isn't Paul, hit several balls into the woods and 
  //    scored ten strokes more than the pro-shop clerk.
  m.post(Clubb != Paul);
  m.post(score[Clubb] == score[clerk] + 10);
   

  // 3. In some order, Frank and the caddy scored four and seven more 
  //    strokes than Mr. Sands.
  m.post(Frank != caddy);
  m.post(Frank != Sands);
  m.post(caddy != Sands);
  m.post(
    (score[Frank] == score[Sands] + 4 &&
     score[caddy] == score[Sands] + 7 )
    ||
    (score[Frank] == score[Sands] + 7 &&
     score[caddy] == score[Sands] + 4 )
         );
  
  // 4. Mr. Carter thought his score of 78 was one of his better games, even 
  // though Frank's score was lower.
  m.post(Frank != Carter);
  m.post(score[Carter] == 78);
  m.post(score[Frank] < score[Carter]);

  // 5. None of the four scored exactly 81 strokes.
  forall(i in R) 
    m.post(score[i] != 81);
   
} using {

  labelFF(last_name);
  labelFF(job);
  labelFF(score);

  cout << first_name << endl;
  cout << last_name << endl;
  cout << job << endl;
  cout << score << endl;
  cout << endl; 

}


cout << "\nnum_solutions: " << num_solutions << endl;
    
int t1 = System.getCPUTime();
cout << "time:      " << (t1-t0) << endl;
cout << "#choices = " << m.getNChoice() << endl;
cout << "#fail    = " << m.getNFail() << endl;
cout << "#propag  = " << m.getNPropag() << endl;
