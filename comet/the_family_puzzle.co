/*

  The Familiy Puzzle in Comet.
  
  From Drools Puzzle Round 2: The Familiy Puzzle
  http://blog.athico.com/2007/08/drools-puzzle-round-2-familiy-puzzle.html
  """
 
  * Three men, Abel, Locker and Snyder are married to Edith, Doris and Luisa, 
    but not necessarily in this order.
  * Each couple has one son.
  * The sons are called Albert, Henry and Victor.
  * Snyder is nor married to Luisa, neither is he Henry's father.
  * Edit is not married to Locker and not Albert's mother.
  * If Alberts father is either Locker or Snyder, then Luisa is Victor's mother.
  * If Luisa is married to Locker, then Doris is not Albert's mother. 
  
  Who is married to whom and what are their sons called?

  Taken from the German book "Denken als Spiel" by Willy Hochkeppel, 1973 
  (Thinking as a Game).  
  """

  Solutions and discussions
  http://ningning.org/blog2/2008/05/25/drools-puzzles-result-round-2-the-familiy-puzzle
  http://rbs.gernotstarke.de/samples/page21/page21.html

  This Comet model was created by Hakan Kjellerstrand (hakank@bonetmail.com)
  Also, see my Comet page: http://www.hakank.org/comet 

*/

import cotfd;

int t0 = System.getCPUTime();

int n = 3;
range R = 1..n;

Solver<CP> m();

int Abel = 1;
int Locker = 2;
int Snyder = 3;
int men[R] = [Abel, Locker, Snyder];

var<CP>{int} Edith(m, R);
var<CP>{int} Doris(m, R);
var<CP>{int} Luisa(m, R);
// Note: This is a gotcha.
// The following declaration is _not_ correct in this context,
// since it will generate 27 solutions, all the same.
// var<CP>{int} women[R](m, R);
// This is the proper way of declaration, i.e. no (n, R)
var<CP>{int} women[R];
women[1] = Edith; women[2] = Doris; women[3] = Luisa;

var<CP>{int} Albert(m, R);
var<CP>{int} Henry(m, R);
var<CP>{int} Victor(m, R);
var<CP>{int} sons[R];
sons[1] = Albert; sons[2] = Henry; sons[3] = Victor;

Integer num_solutions(0);

exploreall<m> {

  m.post(alldifferent(women));
  m.post(alldifferent(sons));

  // Snyder is nor married to Luisa, neither is he Henry's father.
  m.post(Snyder != Luisa);
  m.post(Snyder != Henry);

  // Edith is not married to Locker and not Albert's mother.
  m.post(Edith != Locker);
  m.post(Edith != Albert);

  // If Alberts father is either Locker or Snyder, 
  // then Luisa is Victor's mother.
  m.post(
         (Albert == Locker || Albert == Snyder) => 
         Luisa == Victor
         );

  // If Luisa is married to Locker, 
  // then Doris is not Albert's mother. 
  m.post(
         Luisa == Locker => 
         Doris != Albert);


} using {
      
  labelFF(m);

  num_solutions++;

  /*
  cout << Abel << " " << Locker << " " << Snyder << endl;
  cout << Edith << " " << Doris << " " << Luisa << endl;
  cout << Albert << " " << Henry << " " << Victor << endl;
  */

  cout << men << endl;
  cout << women << endl;
  cout << sons << endl;

  cout << endl;

}

cout << "\nnum_solutions: " << num_solutions << endl;
    
int t1 = System.getCPUTime();
cout << "time:      " << (t1-t0) << endl;
cout << "#choices = " << m.getNChoice() << endl;
cout << "#fail    = " << m.getNFail() << endl;
cout << "#propag  = " << m.getNPropag() << endl;

