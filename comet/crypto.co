/*

  Crypto problem in Comet.

  
  This is a standard benchmark "crypto" problem.
 
  From GLPK:s model cryto.mod.
 
  """
     This problem comes from the newsgroup rec.puzzle.
     The numbers from 1 to 26 are assigned to the letters of the alphabet.
     The numbers beside each word are the total of the values assigned to
     the letters in the word (e.g. for LYRE: L, Y, R, E might be to equal
     5, 9, 20 and 13, or any other combination that add up to 47).
     Find the value of each letter under the equations:
 
     BALLET  45     GLEE  66     POLKA      59     SONG     61
     CELLO   43     JAZZ  58     QUARTET    50     SOPRANO  82
     CONCERT 74     LYRE  47     SAXOPHONE 134     THEME    72
     FLUTE   30     OBOE  53     SCALE      51     VIOLIN  100
     FUGUE   50     OPERA 65     SOLO       37     WALTZ    34
 
     Solution:
     A, B,C, D, E,F, G, H, I, J, K,L,M, N, O, P,Q, R, S,T,U, V,W, X, Y, Z
     5,13,9,16,20,4,24,21,25,17,23,2,8,12,10,19,7,11,15,3,1,26,6,22,14,18
 
     Reference:
     Koalog Constraint Solver <http://www.koalog.com/php/jcs.php>,
     Simple problems, the crypto-arithmetic puzzle ALPHACIPHER.
  """


  Compare with the MiniZinc model http://www.hakank.org/minizinc/crypto.mzn


  This Comet model was created by Hakan Kjellerstrand (hakank@bonetmail.com)
  Also, see my Comet page: http://www.hakank.org/comet 

 */

import cotfd;

int t0 = System.getCPUTime();

int num_letters = 26;
range Letters = 1..num_letters;

int BALLET     =  45;
int CELLO      =  43;
int CONCERT    =  74;
int FLUTE      =  30;
int FUGUE      =  50;
int GLEE       =  66;
int JAZZ       =  58;
int LYRE       =  47;
int OBOE       =  53;
int OPERA      =  65;
int POLKA      =  59;
int QUARTET    =  50;
int SAXOPHONE  = 134;
int SCALE      =  51;
int SOLO       =  37;
int SONG       =  61;
int SOPRANO    =  82;
int THEME      =  72;
int VIOLIN     = 100;
int WALTZ      =  34;

enum letters = {dummy, A,B,C,D,E,F,G,H,I,J,K,L,M,N,O,P,Q,R,S,T,U,V,W,X,Y,Z};

Solver<CP> cp();

/*
var<CP>{int} A(cp, Letters);
var<CP>{int} B(cp, Letters);
var<CP>{int} C(cp, Letters);
var<CP>{int} D(cp, Letters);
var<CP>{int} E(cp, Letters);
var<CP>{int} F(cp, Letters);
var<CP>{int} G(cp, Letters);
var<CP>{int} H(cp, Letters);
var<CP>{int} I(cp, Letters);
var<CP>{int} J(cp, Letters);
var<CP>{int} K(cp, Letters);
var<CP>{int} L(cp, Letters);
var<CP>{int} M(cp, Letters);
var<CP>{int} N(cp, Letters);
var<CP>{int} O(cp, Letters);
var<CP>{int} P(cp, Letters);
var<CP>{int} Q(cp, Letters);
var<CP>{int} R(cp, Letters);
var<CP>{int} S(cp, Letters);
var<CP>{int} T(cp, Letters);
var<CP>{int} U(cp, Letters);
var<CP>{int} V(cp, Letters);
var<CP>{int} W(cp, Letters);
var<CP>{int} X(cp, Letters);
var<CP>{int} Y(cp, Letters);
var<CP>{int} Z(cp, Letters);
*/

var<CP>{int} x[Letters](cp, Letters);
/*
x[1]  = A; x[2]  = B; x[3]  = C; x[4]  = D; x[5]  = E; x[6]  = F; 
x[7]  = G; x[8]  = H; x[9]  = I; x[10] = J; x[11] = K; x[12] = L; 
x[13] = M; x[14] = N; x[15] = O; x[16] = P; x[17] = Q; x[18] = R; 
x[19] = S; x[20] = T; x[21] = U; x[22] = V; x[23] = W; x[24] = X; 
x[25] = Y; x[26] = Z; 
*/

Integer num_solutions(0);

exploreall<cp> {

  cp.post(                 x[B] + x[A] + x[L] + x[L] + x[E] + x[T] == BALLET, onBounds);
  cp.post(                        x[C] + x[E] + x[L] + x[L] + x[O] == CELLO, onBounds);
  cp.post(          x[C] + x[O] + x[N] + x[C] + x[E] + x[R] + x[T] == CONCERT, onBounds);
  cp.post(                        x[F] + x[L] + x[U] + x[T] + x[E] == FLUTE, onBounds);
  cp.post(                        x[F] + x[U] + x[G] + x[U] + x[E] == FUGUE, onBounds);
  cp.post(                               x[G] + x[L] + x[E] + x[E] == GLEE, onBounds);
  cp.post(                               x[J] + x[A] + x[Z] + x[Z] == JAZZ, onBounds);
  cp.post(                               x[L] + x[Y] + x[R] + x[E] == LYRE, onBounds);
  cp.post(                               x[O] + x[B] + x[O] + x[E] == OBOE, onBounds);
  cp.post(                        x[O] + x[P] + x[E] + x[R] + x[A] == OPERA, onBounds);
  cp.post(                        x[P] + x[O] + x[L] + x[K] + x[A] == POLKA, onBounds);
  cp.post(          x[Q] + x[U] + x[A] + x[R] + x[T] + x[E] + x[T] == QUARTET, onBounds);
  cp.post(x[S] + x[A] + x[X] + x[O] + x[P] + x[H] + x[O] + x[N] + x[E] == SAXOPHONE, onBounds);
  cp.post(                        x[S] + x[C] + x[A] + x[L] + x[E] == SCALE, onBounds);
  cp.post(                               x[S] + x[O] + x[L] + x[O] == SOLO, onBounds);
  cp.post(                               x[S] + x[O] + x[N] + x[G] == SONG, onBounds);
  cp.post(          x[S] + x[O] + x[P] + x[R] + x[A] + x[N] + x[O] == SOPRANO, onBounds);
  cp.post(                        x[T] + x[H] + x[E] + x[M] + x[E] == THEME, onBounds);
  cp.post(                 x[V] + x[I] + x[O] + x[L] + x[I] + x[N] == VIOLIN, onBounds);
  cp.post(                        x[W] + x[A] + x[L] + x[T] + x[Z] == WALTZ, onBounds);

  cp.post(alldifferent(x), onDomains);

} using {
      
  labelFF(x);

  num_solutions := num_solutions + 1;

  cout << x << endl;

}

cout << "\nnum_solutions: " << num_solutions << endl;

int t1 = System.getCPUTime();
cout << "time:      " << (t1-t0) << endl;
cout << "#choices = " << cp.getNChoice() << endl;
cout << "#fail    = " << cp.getNFail() << endl;
cout << "#propag  = " << cp.getNPropag() << endl;

