/*

  Labeled dice in Comet.

  From Jim Orlin "Colored letters, labeled dice: a logic puzzle"
  http://jimorlin.wordpress.com/2009/02/17/colored-letters-labeled-dice-a-logic-puzzle/
  """
  My daughter Jenn bough a puzzle book, and showed me a cute puzzle.  There 
  are 13 words as follows:  BUOY, CAVE, CELT, FLUB, FORK, HEMP, JUDY, 
  JUNK, LIMN, QUIP, SWAG, VISA, WISH.

  There are 24 different letters that appear in the 13 words.  The question 
  is:  can one assign the 24 letters to 4 different cubes so that the 
  four letters of each word appears on different cubes.  (There is one 
  letter from each word on each cube.)  It might be fun for you to try 
  it.  I'll give a small hint at the end of this post. The puzzle was 
  created by Humphrey Dudley.
  """


  This Comet model was created by Hakan Kjellerstrand (hakank@bonetmail.com)
  Also, see my Comet page: http://www.hakank.org/comet 

 */

import cotfd;

int t0 = System.getCPUTime();

int n = 4;
range Cube = 1..n;
int num_words = 13;

enum letters = {dummy,A,B,C,D,E,F,G,H,I,J,K,L,M,N,O,P,Q,R,S,T,U,V,W,Y};
letters words[1..num_words, 1..n] = 
  [
   [B,U,O,Y],
   [C,A,V,E], 
   [C,E,L,T], 
   [F,L,U,B], 
   [F,O,R,K], 
   [H,E,M,P], 
   [J,U,D,Y], 
   [J,U,N,K], 
   [L,I,M,N], 
   [Q,U,I,P], 
   [S,W,A,G], 
   [V,I,S,A], 
   [W,I,S,H]
   ];

Solver<CP> m();

var<CP>{int} dice[1..24](m, Cube);

Integer num_solutions(0);

exploreall<m> {

  // the letters in a word must be on a different die
  forall(i in 1..num_words) 
    m.post(alldifferent(all(j in 1..n) dice[words[i,j]]));

  // there must be exactly 6 letters of each die
  forall(i in 1..n)
    m.post(sum(j in 1..24) (dice[j] == i) == 6);

  // symmetry breaking: reduce from 6 solutions to a single one
  m.post(dice[ 1] <= dice[ 7]);
  m.post(dice[ 7] <= dice[13]);
  m.post(dice[13] <= dice[19]);

} using {

  labelFF(m);

  num_solutions := num_solutions + 1;

  cout << "letters -> cube: " << endl;
  forall(i in letters : i != dummy) {
    cout << i << ": " << dice[i] << endl;
  }

  cout << "\nwords with cube label: " << endl;
  forall(i in 1..num_words) {
    forall(j in 1..n) {
      cout << words[i,j] << "(" << dice[words[i,j]] << ") ";
    }
    cout << endl;
  }
  cout << endl;

  cout << "Letter: "<< endl;
  forall(s in 1..6) {
    forall(i in letters : i != dummy) 
      if (dice[i] == s) {
        cout << i << " ";
      }
    cout << endl;
  }
  cout << endl;

}

cout << "\nnum_solutions: " << num_solutions << endl;
    
int t1 = System.getCPUTime();
cout << "time:      " << (t1-t0) << endl;
cout << "#choices = " << m.getNChoice() << endl;
cout << "#fail    = " << m.getNFail() << endl;
cout << "#propag  = " << m.getNPropag() << endl;

