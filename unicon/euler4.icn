#
# Euler problem 4
#
# """
# A palindromic number reads the same both ways. The largest palindrome made 
# from the product of two 2-digit numbers is 9009 = 91 Ã— 99.
#
# Find the largest palindrome made from the product of two 3-digit numbers.
# """
#
# This program was written by Hakan Kjellerstrand (hakank@bonetmail.com)
# Also see my Icon/Unicon page http://www.hakank.org/icon/
#
procedure main()
   problem4()
end

procedure problem4()

    local S := set()
    m := 100
    n := 999
    every insert(S, numeric(palindrome(string( (m to n) * (m to n)))))
    L := []
    every push(L, !S) # converts to a list
    write(myMax(L))
end

procedure myMax(L)
    local maximum := get(L)
    every maximum <:= !L
    return(maximum)
end

procedure palindrome(s)
    if s == reverse(s) then return s   
end
