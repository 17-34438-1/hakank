# 
# Problem 32
# """
# We shall say that an n-digit number is pandigital if it makes use of all 
# the digits 1 to n exactly once; for example, the 5-digit number, 15234, is 
# 1 through 5 pandigital.
# 
# The product 7254 is unusual, as the identity, 39 Ã— 186 = 7254, containing 
# multiplicand, multiplier, and product is 1 through 9 pandigital.
# 
# Find the sum of all products whose multiplicand/multiplier/product identity 
# can be written as a 1 through 9 pandigital.
# HINT: Some products can be obtained in more than one way so be sure 
# to only include it once in your sum.
# """ 
#
# This program was written by Hakan Kjellerstrand (hakank@gmail.com)
# Also see my Icon/Unicon page http://www.hakank.org/icon/
#
link lists
procedure main()
    problem32()
    # problem32b()
end

#
# another approach, rather faster
# 
procedure problem32()
    s := set()
    every a:= 2 to 98 do {
        every b := a + 1 to 9876  do {
            prod := a*b

            ll := a || b || prod # make it a string
            *ll = 9 | next
            find("0", ll) & next

            # ensure unicity
            ss := set()
            every insert(ss, !ll)
            *ss = 9 | next
            # member(ss, "0") & next

            # write(a, "*", b, "=", prod)
            insert(s, prod)
        }
    }
    
    total := 0
    every total +:= !s
    write(total)
end


#
# permutation approach, slower
#
procedure problem32b()

    L:=[1,2,3,4,5,6,7,8,9]
    products := set()
    every p := lpermute(L) do {
        A:=p[1]; B:=p[2]; C:=p[3]; D:=p[4]
        E:=p[5]; F:=p[6]; G:=p[7]; H:=p[8]
        I:=p[9]
        
        if (A*10+B) * (C*100+D*10+E) = (F*1000+G*100+H*10+I) then {
            write(A,B,"*",C,D,E,"=",F,G,H,I)
            insert(products, (F*1000+G*100+H*10+I))
        } else if (A) * (B*1000+C*100+D*10+E) = (F*1000+G*100+H*10+I) then {
            write(A,"*",B,C,D,E,"=",F,G,H,I)
            insert(products, (F*1000+G*100+H*10+I))
        }
    }
 
    s := 0
    every writes(s+:=!products," ")
    write("sum: ", s)

end


