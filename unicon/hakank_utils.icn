#
# Hakan Kjellerstrand (hakank@bonetmail.com)
# 
# Some utilities.
#
#  split(char, str)    : split string on char  
#  split(chars, str)   : split string on chars
#  split(n, str)       : split string into n length segments 
#                        the last may be shorter
#




#
# split on one or many chars 
#          or on a number n: each string is of n length (except the last)
#
# note the two constructs 
#   "| 0"
#   move(1) | break

link ximage, patterns

#
# is called splitx so it's not colliding with split() i the qei program
#
procedure splitx(str, chars) 
  local vec

    vec := []



    if *chars = 0 then {
        # split on ""
        every put(vec, !str)

    } else if integer(chars) then {
        # split on number (positions)
        str ? {
            repeat {
                # this works but can probably be better
                put(vec, move(chars)) | (put(vec, tab(0)) & break)
            }
        }

    } else {
        # split on char or chars
        str ? {
            repeat {
                # tab either to upto(chars) or to end of line (position 0)
                put(vec, tab(upto(chars) | 0))
                move(1) | break
            } 
        }
    }
    
    return vec

end
#
# end splitx
#
