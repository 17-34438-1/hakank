#
# Euler problem 45
#
# """
# Triangle, pentagonal, and hexagonal numbers are generated by the following formulae:
#
# Triangle 	  	Tn=n(n+1)/2 	  	1, 3, 6, 10, 15, ...
# Pentagonal 	  	Pn=n(3nâˆ’1)/2 	  	1, 5, 12, 22, 35, ...
# Hexagonal 	  	Hn=n(2nâˆ’1) 	  	1, 6, 15, 28, 45, ...
# 
# It can be verified that T(285) = P(165) = H(143) = 40755.
# 
# Find the next triangle number that is also pentagonal and hexagonal.
# """
#
# This program was written by Hakan Kjellerstrand (hakank@gmail.com)
# Also see my Icon/Unicon page http://www.hakank.org/icon/
#
link factors, lists, strings

procedure main()
   problem45()
end

procedure problem45()

    tt := tri(t:=285+1)
    pp := pent(p:=165)
    hh := hex(h:=143)

    while 1 do {
        if tt = pp = hh then break

        tt := tri(t +:= 1)
        if tt > pp then pp := pent(p +:= 1) 
        if pp > hh then hh := hex(h +:= 1)
        if tt > hh then hh := hex(h +:= 1)       

    }
    # write("tri:", t, ") = ", tri(t), " pent(",p, ") = ", pent(p), " hex(",h, ") = ", hex(h))
    write(tt)
end

procedure tri(n)
    return n*(n+1)/2
end

procedure pent(n)
    return n*(3*n-1)/2
end

procedure hex(n)
    return n*(2*n-1)
end

