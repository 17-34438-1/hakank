#
# Euler problem 44
#
# """
# Pentagonal numbers are generated by the formula, P(n)=n(3n−1)/2. 
# The first ten pentagonal numbers are:
# 
# 1, 5, 12, 22, 35, 51, 70, 92, 117, 145, ...
# 
# It can be seen that P(4) + P(7) = 22 + 70 = 92 = P(8). However, their difference, 
# 70 − 22 = 48, is not pentagonal.
# 
# Find the pair of pentagonal numbers, P(j) and P(k), for which their sum and difference 
# is pentagonal and D = |P(k) − P(j)| is minimised; what is the value of D?
# """
#
# This program was written by Hakan Kjellerstrand (hakank@bonetmail.com)
# Also see my Icon/Unicon page http://www.hakank.org/icon/
#
link factors, lists, strings

procedure main()
   problem44()
end



procedure problem44()
    t := table()
    s := set()
    every n := 1 to 2500 do {
        p := pent(n)
        t[p] := n
        insert(s, p) 
    }
    
    d := 100000000
    every j := !s & k := !s & j <= k do {
        a := j + k
        if not member(s, a) then next
        b := abs(j-k)
        if not member(s, b) then next
        d >:= b
    }
    write("d:", d)

end

procedure pent(n)
    return n*(3*n-1)/2
end
