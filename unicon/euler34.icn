# 
# Problem 34
# """
# 145 is a curious number, as 1! + 4! + 5! = 1 + 24 + 120 = 145.
# 
# Find the sum of all numbers which are equal to the sum of the 
# factorial of their digits.
# 
# Note: as 1! = 1 and 2! = 2 are not sums they are not included.
# """ 
#
# This program was written by Hakan Kjellerstrand (hakank@bonetmail.com)
# Also see my Icon/Unicon page http://www.hakank.org/icon/
#
link factors
procedure main()
    problem34()
end

procedure problem34()
    s := 0
    every i := 3 to 50000 do {
        f := 0
        every f +:= my_factorial(!i)
        i = f & write(i) & s +:= i
    }
    write("sum:", s)

end


procedure my_factorial(n)
    static h := table()
    return (member(h, n) & h[n]) | (h[n] := factorial(n))
end

