# 
# Problem 30
# """
# Surprisingly there are only three numbers that can be written as the sum of 
# fourth powers of their digits:
#
#     1634 = 1^4 + 6^4 + 3^4 + 4^4
#     8208 = 8^4 + 2^4 + 0^4 + 8^4
#     9474 = 9^4 + 4^4 + 7^4 + 4^4
#
# As 1 = 1^4 is not a sum it is not included.
#
# The sum of these numbers is 1634 + 8208 + 9474 = 19316.
#
# Find the sum of all the numbers that can be written as the sum of fifth 
# powers of their digits.
#
# """ 
#
# This program was written by Hakan Kjellerstrand (hakank@bonetmail.com)
# Also see my Icon/Unicon page http://www.hakank.org/icon/
#

link numbers

procedure main()
    problem30()
end

procedure problem30()
    p := 5
    s := set()
    every n := 5 to 7*9^p & check(n,p) do insert(s, n)
    every writes(!sort(s), " ")
    # every (/t := 0) +:=!s
    # write("sum: ", t)
    write("sum: ", sum2(s))
end


procedure check(n, p) 
    every (/s := 0) +:= !n^p
    return s == n
end


procedure sum2(A)
    every (/t:=0) +:=!A
    return t
end
