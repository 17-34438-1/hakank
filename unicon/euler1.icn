#
# Euler problem 1
#
# """
# If we list all the natural numbers below 10 that are multiples of 3 or 5, 
# we get 3, 5, 6 and 9. The sum of these multiples is 23.
# Find the sum of all the multiples of 3 or 5 below 1000.
# """
#
# This program was written by Hakan Kjellerstrand (hakank@gmail.com)
# Also see my Icon/Unicon page http://www.hakank.org/icon/
#
procedure main()
   problem1()
   problem1b()
end

procedure problem1()
  s := 0;
  every s +:= mult3_or_5(1 to 999)
  write(s)

end


procedure mult3_or_5(n)
  # if n % 3 == 0 | n % 5 == 0 then
  if n % (3|5) == 0 then
    suspend n
end


procedure problem1b()
  s := 0;
  every n := 1 to 999 do {
     if n % (3|5) == 0 then { s +:= n }
  }
  write(s)

end

