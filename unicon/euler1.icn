#
# Euler problem 1
#
# """
# If we list all the natural numbers below 10 that are multiples of 3 or 5, 
# we get 3, 5, 6 and 9. The sum of these multiples is 23.
# Find the sum of all the multiples of 3 or 5 below 1000.
# """
#
# This program was written by Hakan Kjellerstrand (hakank@gmail.com)
# Also see my Icon/Unicon page http://www.hakank.org/icon/
#
link numbers
procedure main()
    problem1()
    # problem1b()
    # problem1c()
    # problem1d()
end

procedure problem1()
    s := 0;
    every s +:= mult3_or_5(1 to 999)
    write(s)
    
end


procedure mult3_or_5(n)
    if n % (3|5) == 0 then suspend n
end
        
        
procedure problem1b()
    s := 0;
    every n := 1 to 999 do {
        if n % (3|5) == 0 then s +:= n
    }
    write(s) 
end


# list comprehension
procedure problem1c()
    write(sum![: n := 1 to 999 & if n % (3|5) = 0 then n else 0 :])
end

# list comprehension
# need to constrain to find just one solution with \1
# also (...,n) is so the expression returns n.
procedure problem1d()
    s := [: n := 1 to 999 & (n % (3|5) = 0,n)\1 :]
    write(sum!s)
end

