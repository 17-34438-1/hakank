# 
# Problem 42
# """
# The nth term of the sequence of triangle numbers is given by, tn = 1/2*n*(n+1); 
# so the first ten triangle numbers are:
# 
# 1, 3, 6, 10, 15, 21, 28, 36, 45, 55, ...
# 
# By converting each letter in a word to a number corresponding to its 
# alphabetical position and adding these values we form a word value. For example, 
# the word value for SKY is 19 + 11 + 25 = 55 = t10. If the word value 
# is a triangle number then we shall call the word a triangle word.
# 
# Using words.txt (right click and 'Save Link/Target As...'), a 16K text file 
# containing nearly two-thousand common English words, how many are triangle words?
#
# """ 
#
# This program was written by Hakan Kjellerstrand (hakank@bonetmail.com)
# Also see my Icon/Unicon page http://www.hakank.org/icon/
#

procedure main()
    problem42()
end

procedure problem42()

    T := table()
    # every n := 1 to 20 do T[t:=integer(0.5*n*(n+1))] := 1

    # another version: just take 20 triangle_numbers()
    every T[triangle_number() \ 20] := 1

    c:=0
    str := read(open("words.txt"))
    maxw := 0
    str ? while tab(upto('"')) do {
        move(1)
        s:=tab(upto('"'))
        tab(upto(","))
        member(T, sum_word(s)) & c+:= 1
        # maxw <:= sum_word(s) # check max word sum
    }
    
    write(c)
    # write("maxw: ", maxw)

end

procedure sum_word(w) 
   every (/s := 0) +:= ord(!w)-64 
   return s
end


procedure triangle_number() 
    n := 1
    repeat {
        suspend integer(0.5*n*(n+1))
        n +:= 1
    }
end
