#
# Project Euler problem 22
# """
# """
# Using names.txt (right click and 'Save Link/Target As...'), a 46K 
# text file containing over five-thousand first names, begin by sorting 
# it into alphabetical order. Then working out the alphabetical value 
# for each name, multiply this value by its alphabetical position in the 
# list to obtain a name score.
#
# For example, when the list is sorted into alphabetical order, COLIN, 
# which is worth 3 + 15 + 12 + 9 + 14 = 53, is the 938th name in 
# the list. So, COLIN would obtain a score of 938 53 = 49714.
#
# What is the total of all the name scores in the file?")
# """
#
# This program was written by Hakan Kjellerstrand (hakank@gmail.com)
# Also see my Icon/Unicon page http://www.hakank.org/icon/
#
link numbers, factors
procedure main()
  problem22()
end

procedure problem22()
    local f, s, vv, total, i
    f := open("euler_names.txt")
    s := read(f)
    vv := []
    # remove '"' and split on ","
    s ? while tab(upto('"')) do {
        move(1)
        put(vv, tab(upto('"')))
        tab(upto(","))
    }
    vv := sort(vv)

    total := 0
    i := 0
    every n := !vv do total +:= (i +:= 1)*sum_word(n)
    write(total)
end

procedure sum_word(w) 
   local s := 0
   every s +:= ord(!w)-64 
   return s
end
