%
% SEND + MORE + MONEY in any base in Zinc
%
% Send More Money
%    SEND
%  + MORE
%  ------
%   MONEY
%
% This Zinc program is written by Hakan Kjellerstrand
% See also my MiniZinc page: http://www.hakank.org/minizinc
%

include "globals.zinc";

int: base = 10;
type digits = var 0..base-1;
digits: S;
digits: E;
digits: N;
digits: D;
digits: M;
digits: O;
digits: R;
digits: Y;

array[1..8] of var int : FD = [S,E,N,D,M,O,R,Y];


constraint
  alldifferent(FD) /\
                          pow(base,3)*S + pow(base,2)*E + base*N + D  +  
                          pow(base,3)*M + pow(base,2)*O + base*R + E  = 
          pow(base,4)*M + pow(base,3)*O + pow(base,2)*N + base*E + Y 

  /\
  S >= 1 /\
  M >= 1
;  


solve satisfy;

output [
   "S:", show(S), " E:", show(E), " N:", show(N), " D:", show(D),
   " M:", show(M), " O:", show(O), " R:", show(R), " Y:", show(Y),
   "\n\n",

   "    ", show(S), " ", show(E), " ", show(N), " ", show(D),"\n",
   " +  ", show(M), " ", show(O), " ", show(R), " ", show(E),"\n",
   " = ",  show(M), " ", show(O), " ", show(N), " ", show(E), " ", show(Y), "\n"
];
