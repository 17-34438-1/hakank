%
% Send more money problem in Zzinc.
%
% Send More Money
%    SEND
%  + MORE
%  ------
%   MONEY
%
% This Zinc model was written by Hakan Kjellerstrand.
% See also my MiniZinc page: http://www.hakank.org/minizinc
%

include "globals.zinc";
enum digits = {S,E,N,D,M,O,R,Y};
array[digits] of var 0..9: x;

constraint
  alldifferent(x) /\
            1000*x[S] + 100*x[E] + 10*x[N] + x[D]  +  
            1000*x[M] + 100*x[O] + 10*x[R] + x[E]  = 
  10000*x[M] + 1000*x[O] + 100*x[N] + 10*x[E] + x[Y] 
  /\
  x[S] > 0 /\ x[M] > 0
;  


solve satisfy;

output [
   "x: ", show(x), "\n",
   "\n",
   "    ", show(x[S]), show(x[E]), show(x[N]), show(x[D]),"\n",
   " +  ", show(x[M]), show(x[O]), show(x[R]), show(x[E]),"\n",
   " = ",  show(x[M]), show(x[O]), show(x[N]), show(x[E]), show(x[Y]), "\n"
];
