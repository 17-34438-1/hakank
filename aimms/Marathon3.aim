
MAIN MODEL Main_Marathon3

  DECLARATION SECTION 

    SET:
       identifier   :  NameSet
       index        :  r
       definition   :  data {Dominique, Ignace, Naren, Olivier, Philippe, Pascal} ;

    SET:
       identifier   :  Positions
       subset of    :  Integers
       index        :  p
       definition   :  {1..n} ;

    PARAMETER:
       identifier   :  n
       definition   :  card(NameSet) ;

    ELEMENT VARIABLE:
       identifier   :  x
       index domain :  r
       range        :  Positions
       comment      :  "For each runner, its position." ;

    ELEMENT VARIABLE:
       identifier   :  RunnerPerPosition
       index domain :  p
       range        :  NameSet
       comment      :  "For each position, which runner" ;

    CONSTRAINT:
       identifier   :  alldiff
       definition   :  cp::AllDifferent(r, x(r)) ;

    CONSTRAINT:
       identifier   :  inverse
       definition   :  cp::Channel(r, x(r), p, RunnerPerPosition(p))
       comment      :  "The positions." ;

    CONSTRAINT:
       identifier   :  c1
       definition   :  x('Olivier') <> 6
       comment      :  "Olivier not last" ;

    CONSTRAINT:
       identifier   :  c2
       definition   :  x('Dominique') < x('Naren') and
                       x('Dominique') < x('Olivier') and
                       
                       x('Pascal') < x('Naren') and
                       x('Pascal') < x('Olivier')and
                       
                       x('Ignace') < x('Naren') and
                       x('Ignace') < x('Olivier')
       comment      :  "Dominique, Pascal and Ignace before Naren and Olivier" ;

    CONSTRAINT:
       identifier   :  c3
       definition   :  x('Dominique') < 3
       comment      :  "Dominique better than third" ;

    CONSTRAINT:
       identifier   :  c4
       definition   :  x('Philippe') <= 4
       comment      :  "Philippe is amongh the first four" ;

    CONSTRAINT:
       identifier   :  c5
       definition   :  x('Ignace') <> 2 and
                       x('Ignace') <> 3
       comment      :  "Ignace neither second nor third" ;

    CONSTRAINT:
       identifier   :  c6
       definition   :  x('Pascal') + 3 = x('Naren')
       comment      :  "Pascal three places earlier than Naren" ;

    CONSTRAINT:
       identifier   :  c7
       definition   :  x('Ignace') <> 4 and
                       x('Dominique') <> 4
       comment      :  "Neither Ignace nor Dominique on fourth position" ;

    MATHEMATICAL PROGRAM:
       identifier   :  Marathon3Plan
       direction    :  minimize
       constraints  :  AllConstraints
       variables    :  AllVariables
       type         :  CSP ;

  ENDSECTION  ;

  PROCEDURE
    identifier :  MainInitialization

  ENDPROCEDURE  ;

  PROCEDURE
    identifier :  MainExecution
    body       :  
      ShowProgressWindow;
      solve Marathon3Plan;
      
      DialogMessage("It was " + GMP::Solution::Count('Marathon3Plan') + " solution(s)");
      
      display x, RunnerPerPosition;

  ENDPROCEDURE  ;

  PROCEDURE
    identifier :  MainTermination
    body       :  
      return DataManagementExit();

  ENDPROCEDURE  ;

ENDMODEL Main_Marathon3 ;
