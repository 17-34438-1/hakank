
MAIN MODEL Main_AllDifferentExcept0

  DECLARATION SECTION 

    PARAMETER:
       identifier   :  N
       initial data :  6 ;

    PARAMETER:
       identifier   :  NumZeros
       initial data :  2 ;

    SET:
       identifier   :  ii
       indices      :  i, j
       definition   :  ElementRange(1, N) ;

    VARIABLE:
       identifier   :  x
       index domain :  (i)
       range        :  {0..N} ;

    VARIABLE:
       identifier   :  z
       range        :  {0..N}
       definition   :  Sum[i, x(i) = 0]
       comment      :  "Number of zeros in x." ;

    MATHEMATICAL PROGRAM:
       identifier   :  AllDifferentExcept0Plan
       direction    :  minimize
       constraints  :  AllConstraints
       variables    :  AllVariables
       type         :  Automatic ;

    CONSTRAINT:
       identifier   :  CAllDifferentExcept0
       index domain :  (i,j) | i < j
       definition   :  if (x(i) <> 0 and x(j) <> 0) then
                          x(i) <> x(j)
                       endif  ;

    CONSTRAINT:
       identifier   :  CIncreasing
       index domain :  i | ord(i) > 1
       definition   :  x(i-1) <= x(i)
       comment      :  "Implements the increasing constraint. TODO: make it a procedure/function." ;

    CONSTRAINT:
       identifier   :  CountZeros
       definition   :  z = NumZeros ;

  ENDSECTION  ;

  PROCEDURE
    identifier :  MainInitialization

  ENDPROCEDURE  ;

  PROCEDURE
    identifier :  MainExecution
    body       :  
      ShowProgressWindow;
      solve AllDifferentExcept0Plan;
      
      if (AllDifferentExcept0Plan.ProgramStatus <> 'Optimal') then
           empty x, z;
      endif;

  ENDPROCEDURE  ;

  PROCEDURE
    identifier :  MainTermination
    body       :  
      if ( CaseSaveAll( confirm:2 ) = 1 ) then
          return 1;
      else
          return 0;
      endif ;

  ENDPROCEDURE  ;

ENDMODEL Main_AllDifferentExcept0 ;
