
MAIN MODEL Main_CoinsGridCP

  DECLARATION SECTION 

    PARAMETER:
       identifier   :  N
       initial data :  10 ;

    PARAMETER:
       identifier   :  C
       initial data :  4
       comment      :  "Number of coins per row/column" ;

    VARIABLE:
       identifier   :  z
       range        :  free
       definition   :  Sum[(i,j), x(i,j)*(Abs(i-j))*(Abs(i-j))]
       comment      :  "Sum of quadratic horizontal distance" ;

    CONSTRAINT:
       identifier   :  SumRows
       index domain :  i
       definition   :  Sum[j, x(i,j)] = C ;

    CONSTRAINT:
       identifier   :  SumCols
       index domain :  (j)
       definition   :  Sum[i, x(i,j)] = C ;

    MATHEMATICAL PROGRAM:
       identifier   :  CoinsGridCPMIPPlan
       objective    :  z
       direction    :  minimize
       constraints  :  AllConstraints
       variables    :  AllVariables
       type         :  selectedModelClass ;

    VARIABLE:
       identifier   :  x
       index domain :  (i,j)
       range        :  binary ;

    SET:
       identifier   :  ij
       subset of    :  Integers
       indices      :  i, j
       definition   :  {1..N} ;

    SET:
       identifier   :  CombinatorialOptimizationModelClasses
       subset of    :  AllMathematicalProgrammingTypes
       parameter    :  selectedModelClass
       definition   :  data { MIP, COP } ;

    PARAMETER:
       identifier   :  solutionTimeLimit
       initial data :  60
       comment      :  "in seconds" ;

  ENDSECTION  ;

  PROCEDURE
    identifier :  MainInitialization
    body       :  
      selectedModelClass := 'COP' ;

  ENDPROCEDURE  ;

  PROCEDURE
    identifier :  MainExecution
    body       :  
      ShowProgressWindow ;
      solve CoinsGridCPMIPPlan where time_limit := solutionTimeLimit ;

  ENDPROCEDURE  ;

  PROCEDURE
    identifier :  MainTermination
    body       :  
      return 1;

  ENDPROCEDURE  ;

ENDMODEL Main_CoinsGridCP ;
