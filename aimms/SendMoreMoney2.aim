
MAIN MODEL Main_SendMoreMoney2

  DECLARATION SECTION 

    SET:
       identifier   :  ii
       subset of    :  Integers
       index        :  i
       definition   :  {1,2,3,4,5,6,7,8} ;

    CONSTRAINT:
       identifier   :  SendMoreMoney
       definition   :  (1000*x(S) + 100*x(E)+ 10*x(N) + x(D) + 1000*x(M) + 100*x(O)+ 10*x(R) + x(E)) = (10000*x(M) + 1000*x(O) + 100*x(N) + 10*x(E) + x(Y)) ;

    MATHEMATICAL PROGRAM:
       identifier   :  SendMoreMoney2Plan
       direction    :  minimize
       constraints  :  AllConstraints
       variables    :  AllVariables
       type         :  CSP ;

    CONSTRAINT:
       identifier   :  SM
       definition   :  x(S) > 0 and x(M) > 0  ;

    VARIABLE:
       identifier   :  x
       index domain :  (i)
       range        :  {0..9} ;

    PARAMETER:
       identifier   :  S
       definition   :  1 ;

    PARAMETER:
       identifier   :  E
       definition   :  2 ;

    PARAMETER:
       identifier   :  N
       definition   :  3 ;

    PARAMETER:
       identifier   :  D
       definition   :  4 ;

    PARAMETER:
       identifier   :  M
       definition   :  5 ;

    PARAMETER:
       identifier   :  O
       definition   :  6 ;

    PARAMETER:
       identifier   :  R
       definition   :  7 ;

    PARAMETER:
       identifier   :  Y
       definition   :  8 ;

    CONSTRAINT:
       identifier   :  AllDiff
       definition   :  cp::AllDifferent(i, x(i)) ;

  ENDSECTION  ;

  PROCEDURE
    identifier :  MainInitialization

  ENDPROCEDURE  ;

  PROCEDURE
    identifier :  MainExecution
    body       :  
      ShowProgressWindow;
      solve SendMoreMoney2Plan;
      
      if (SendMoreMoney2Plan.ProgramStatus <> 'Optimal') then
        empty x;
      endif;

  ENDPROCEDURE  ;

  PROCEDURE
    identifier :  Empty_x
    body       :  
      empty x;

  ENDPROCEDURE  ;

  PROCEDURE
    identifier :  MainTermination
    body       :  
      if ( CaseSaveAll( confirm:2 ) = 1 ) then
          return 1;
      else
          return 0;
      endif ;

  ENDPROCEDURE  ;

ENDMODEL Main_SendMoreMoney2 ;
