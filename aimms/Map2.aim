
MAIN MODEL Main_Map2

  DECLARATION SECTION 

    SET:
       identifier   :  Countries
       indices      :  c1, c2
       definition   :  data {Belgium, Denmark, France, Germany, Netherlands, Luxembourg} ;

    SET:
       identifier   :  Colors
       index        :  c
       initial data :  data {red, blue, green, yellow} ;

    ELEMENT VARIABLE:
       identifier   :  CountryColor
       index domain :  (c1)
       range        :  Colors ;

    MATHEMATICAL PROGRAM:
       identifier   :  Map2Plan
       direction    :  minimize
       constraints  :  AllConstraints
       variables    :  AllVariables
       type         :  CSP ;

    PARAMETER:
       identifier   :  Connections
       index domain :  (c1,c2)
       definition   :  DATA { (Belgium, [France, Germany, Netherlands, Luxembourg]),
                              (Denmark, [Germany]),
                              (France, [Belgium, Germany, Netherlands, Luxembourg]),
                              (Germany, [Belgium, Denmark, France, Netherlands, Luxembourg]),
                              (Netherlands, [Belgium, France, Germany]),
                              (Luxembourg, [Belgium, Germany]) } ;

    CONSTRAINT:
       identifier   :  DifferentColors
       index domain :  (c1,c2)
       definition   :  if (c1 <> c2 and Connections(c1, c2) = 1) then
                          CountryColor(c1) <> CountryColor(c2)
                       endif  ;

  ENDSECTION  ;

  PROCEDURE
    identifier :  MainInitialization

  ENDPROCEDURE  ;

  PROCEDURE
    identifier :  MainExecution
    body       :  
      ShowProgressWindow;
      solve Map2Plan;
      
      if (Map2Plan.ProgramStatus <> 'Optimal') then
           empty CountryColor;
      endif;

  ENDPROCEDURE  ;

  PROCEDURE
    identifier :  MainTermination
    body       :  
      if ( CaseSaveAll( confirm:2 ) = 1 ) then
          return 1;
      else
          return 0;
      endif ;

  ENDPROCEDURE  ;

ENDMODEL Main_Map2 ;
