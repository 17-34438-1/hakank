
MAIN MODEL Main_Seseman

  DECLARATION SECTION 

    PARAMETER:
       identifier   :  N
       initial data :  3 ;

    PARAMETER:
       identifier   :  BorderSum
       initial data :  9 ;

    PARAMETER:
       identifier   :  NN
       definition   :  N*N ;

    PARAMETER:
       identifier   :  N4
       definition   :  N*N*N*N ;

    PARAMETER:
       identifier   :  SolutionLimit ;

    SET:
       identifier   :  ij
       subset of    :  Integers
       indices      :  i, j
       definition   :  {1..N} ;

    VARIABLE:
       identifier   :  TotalSum
       range        :  {0..N4} ;

    VARIABLE:
       identifier   :  x
       index domain :  (i,j)
       range        :  {0..NN} ;

    CONSTRAINT:
       identifier   :  SumTotal
       definition   :  TotalSum = Sum[(i,j), x(i,j)] ;

    CONSTRAINT:
       identifier   :  BorderSum1
       definition   :  BorderSum = Sum[i, x(i, 1)] ;

    CONSTRAINT:
       identifier   :  BorderSum2
       definition   :  BorderSum = Sum[i, x(i, N)] ;

    CONSTRAINT:
       identifier   :  BorderSum3
       definition   :  BorderSum = Sum[j, x(1,j)] ;

    CONSTRAINT:
       identifier   :  BorderSum4
       definition   :  BorderSum = Sum[j, x(N,j)] ;

    CONSTRAINT:
       identifier   :  BordersLargerThan0
       index domain :  (i,j)
       definition   :  if (i = 1 or j = 1 or i = N or j = N) then
                          x(i,j) >= 1
                       endif ;

    CONSTRAINT:
       identifier   :  MiddleCellsMustBe0
       index domain :  (i,j)
       definition   :  if (i > 1 and i < N and j > 1 and j < N) then
                          x(i,j) = 0
                       endif  ;

    MATHEMATICAL PROGRAM:
       identifier   :  SesemanPlan
       direction    :  minimize
       constraints  :  AllConstraints
       variables    :  AllVariables
       type         :  CSP ;

  ENDSECTION  ;

  PROCEDURE
    identifier :  MainInitialization

  ENDPROCEDURE  ;

  PROCEDURE
    identifier :  MainExecution
    body       :  
      ShowProgressWindow;
      solve SesemanPlan;
      
      if (SesemanPlan.ProgramStatus <> 'Optimal') then
           empty x, TotalSum;
      endif;
      
      if (N >= 3) then
          DialogMessage(GMP::Solution::Count('SesemanPlan'));
      endif;
      
      
      /*OptionGetString('CPOPT122:solution limit', SolutionLimit);*/

  ENDPROCEDURE  ;

  PROCEDURE
    identifier :  MainTermination
    body       :  
      if ( CaseSaveAll( confirm:2 ) = 1 ) then
          return 1;
      else
          return 0;
      endif ;

  ENDPROCEDURE  ;

ENDMODEL Main_Seseman ;
