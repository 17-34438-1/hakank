
MAIN MODEL Main_Marathon

  DECLARATION SECTION 

    PARAMETER:
       identifier   :  n
       definition   :  6 ;

    SET:
       identifier   :  S
       subset of    :  Integers
       indices      :  i, j
       definition   :  {1..n} ;

    SET:
       identifier   :  NameSet ;

    VARIABLE:
       identifier   :  x
       index domain :  (i)
       range        :  {1..n} ;

    VARIABLE:
       identifier   :  positions
       index domain :  (i)
       range        :  {1..n}
       comment      :  "The positions of the runners." ;

    VARIABLE:
       identifier   :  Dominique
       range        :  {1..n} ;

    VARIABLE:
       identifier   :  Ignace
       range        :  {1..n} ;

    VARIABLE:
       identifier   :  Naren
       range        :  {1..n} ;

    VARIABLE:
       identifier   :  Olivier
       range        :  {1..n} ;

    VARIABLE:
       identifier   :  Philippe
       range        :  {1..n} ;

    VARIABLE:
       identifier   :  Pascal
       range        :  {1..n} ;

    CONSTRAINT:
       identifier   :  connectToX
       definition   :  x(1) = Dominique and
                       x(2) = Ignace and
                       x(3) = Naren and
                       x(4) = Olivier and
                       x(5) = Philippe and
                       x(6) = Pascal ;

    CONSTRAINT:
       identifier   :  alldiff
       definition   :  cp::AllDifferent(i, x(i)) ;

    CONSTRAINT:
       identifier   :  inverse
       definition   :  cp::Channel(i, x(i), i, positions(i))
       comment      :  "The positions." ;

    CONSTRAINT:
       identifier   :  c1
       definition   :  Olivier <> n
       comment      :  "Olivier not last" ;

    CONSTRAINT:
       identifier   :  c2
       definition   :  Dominique  < Naren and
                       Dominique  < Olivier and
                       
                       Pascal     < Naren and
                       Pascal     < Olivier and
                       
                       Ignace     < Naren and
                       Ignace     < Olivier
       comment      :  "Dominique, Pascal and Ignace before Naren and Olivier" ;

    CONSTRAINT:
       identifier   :  c3
       definition   :  Dominique < 3
       comment      :  " Dominique better than third" ;

    CONSTRAINT:
       identifier   :  c4
       definition   :  Philippe <= 4
       comment      :  "Philippe is among the first four" ;

    CONSTRAINT:
       identifier   :  c5
       definition   :  Ignace <> 2 and
                       Ignace <> 3
       comment      :  " Ignace neither second nor third" ;

    CONSTRAINT:
       identifier   :  c6
       definition   :  Pascal + 3 = Naren
       comment      :  "Pascal three places earlier than Naren" ;

    CONSTRAINT:
       identifier   :  c7
       definition   :  Ignace <> 4 and
                       Dominique <> 4
       comment      :  "Neither Ignace nor Dominique on fourth position" ;

    MATHEMATICAL PROGRAM:
       identifier   :  MarathonPlan
       direction    :  minimize
       constraints  :  AllConstraints
       variables    :  AllVariables
       type         :  CSP ;

  ENDSECTION  ;

  PROCEDURE
    identifier :  MainInitialization

  ENDPROCEDURE  ;

  PROCEDURE
    identifier :  MainExecution
    body       :  
      ShowProgressWindow;
      solve MarathonPlan;
      
      if (MarathonPlan.ProgramStatus <> 'Optimal') then
        empty x
      endif;
      
      DialogMessage(GMP::Solution::Count('MarathonPlan'));

  ENDPROCEDURE  ;

  PROCEDURE
    identifier :  MainTermination
    body       :  
      return DataManagementExit();

  ENDPROCEDURE  ;

ENDMODEL Main_Marathon ;
