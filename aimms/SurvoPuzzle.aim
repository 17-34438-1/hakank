
MAIN MODEL Main_SurvoPuzzle

  DECLARATION SECTION 

    PARAMETER:
       identifier   :  R
       definition   :  3 ;

    PARAMETER:
       identifier   :  C
       definition   :  6 ;

    PARAMETER:
       identifier   :  RC
       definition   :  R*C ;

    PARAMETER:
       identifier   :  RowSums
       index domain :  (i) ;

    PARAMETER:
       identifier   :  ColSums
       index domain :  (j) ;

    PARAMETER:
       identifier   :  Matrix
       index domain :  (i,j)
       range        :  {0..RC} ;

    CONSTRAINT:
       identifier   :  FillData
       index domain :  (i,j)
       definition   :  if (Matrix(i,j) > 0) then
                         x(i,j) = Matrix(i,j)
                       endif  ;

    CONSTRAINT:
       identifier   :  SumRows
       index domain :  (i)
       definition   :  RowSums(i) = Sum[j, x(i,j)] ;

    CONSTRAINT:
       identifier   :  SumCols
       index domain :  (j)
       definition   :  ColSums(j) = Sum[i, x(i,j)] ;

    CONSTRAINT:
       identifier   :  AllDifferentX
       definition   :  cp::AllDifferent( (i,j), x(i,j)) ;

    MATHEMATICAL PROGRAM:
       identifier   :  SurvoPuzzlePlan
       direction    :  minimize
       constraints  :  AllConstraints
       variables    :  AllVariables
       type         :  CSP ;

    VARIABLE:
       identifier   :  x
       index domain :  (i,j)
       range        :  {1..RC} ;

    SET:
       identifier   :  RowDomain
       subset of    :  Integers
       index        :  i
       definition   :  {1..R} ;

    SET:
       identifier   :  ColumnDomain
       subset of    :  Integers
       index        :  j
       definition   :  {1..C} ;

  ENDSECTION  ;

  PROCEDURE
    identifier :  MainInitialization

  ENDPROCEDURE  ;

  PROCEDURE
    identifier :  MainExecution
    body       :  
      ShowProgressWindow;
      solve SurvoPuzzlePlan;
      
      if (SurvoPuzzlePlan.ProgramStatus <> 'Optimal') then
           empty x;
      endif;

  ENDPROCEDURE  ;

  PROCEDURE
    identifier :  MainTermination
    body       :  
      if ( CaseSaveAll( confirm:2 ) = 1 ) then
          return 1;
      else
          return 0;
      endif ;

  ENDPROCEDURE  ;

ENDMODEL Main_SurvoPuzzle ;
