
MAIN MODEL Main_Map

  DECLARATION SECTION 

    SET:
       identifier   :  Countries
       indices      :  c1, c2
       initial data :  data { Belgium, Denmark, France, Germany, Netherlands, Luxembourg } ;

    PARAMETER:
       identifier   :  MaxColors
       initial data :  4 ;

    PARAMETER:
       identifier   :  Connections
       index domain :  (c1,c2) ;

    VARIABLE:
       identifier   :  Colors
       index domain :  (c1)
       range        :  {1..MaxColors} ;

    MATHEMATICAL PROGRAM:
       identifier   :  MapPlan
       direction    :  minimize
       constraints  :  AllConstraints
       variables    :  AllVariables
       type         :  CSP ;

    CONSTRAINT:
       identifier   :  DifferentColors2
       index domain :  (c1,c2)
       definition   :  if (Connections(c1,c2) = 1) then
                            Colors(c1) <> Colors(c2)
                       endif  ;

    CONSTRAINT:
       identifier   :  SymmetryBreaking1
       index domain :  c1 | c1 = "Belgium"
       definition   :  Colors(c1) = 1 ;

  ENDSECTION  ;

  PROCEDURE
    identifier :  MainInitialization

  ENDPROCEDURE  ;

  PROCEDURE
    identifier :  MainExecution
    body       :  
      ShowProgressWindow;
      solve MapPlan;
      
      if (MapPlan.ProgramStatus <> 'Optimal') then
           empty Colors;
      endif;

  ENDPROCEDURE  ;

  PROCEDURE
    identifier :  MainTermination
    body       :  
      if ( CaseSaveAll( confirm:2 ) = 1 ) then
          return 1;
      else
          return 0;
      endif ;

  ENDPROCEDURE  ;

ENDMODEL Main_Map ;
