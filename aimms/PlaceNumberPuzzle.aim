
MAIN MODEL Main_PlaceNumberPuzzle

  DECLARATION SECTION 

    MATHEMATICAL PROGRAM:
       identifier   :  PlaceNumberPuzzlePlan
       direction    :  minimize
       constraints  :  AllConstraints
       variables    :  AllVariables
       type         :  CSP ;

    PARAMETER:
       identifier   :  n
       initial data :  8 ;

    PARAMETER:
       identifier   :  m
       initial data :  32 ;

    PARAMETER:
       identifier   :  graph
       index domain :  (j,k)
       initial data :  data
                       { (  1, 1 ) : 1,  (  1, 2 ) : 2,  (  2, 1 ) : 1,  (  2, 2 ) : 3,  (  3, 1 ) : 1,  (  3, 2 ) : 4,  (  4, 1 ) : 2,
                         (  4, 2 ) : 1,  (  5, 1 ) : 2,  (  5, 2 ) : 3,  (  6, 1 ) : 2,  (  6, 2 ) : 5,  (  7, 1 ) : 2,  (  7, 2 ) : 6,
                         (  8, 1 ) : 3,  (  8, 2 ) : 2,  (  9, 1 ) : 3,  (  9, 2 ) : 4,  ( 10, 1 ) : 3,  ( 10, 2 ) : 6,  ( 11, 1 ) : 3,
                         ( 11, 2 ) : 7,  ( 12, 1 ) : 4,  ( 12, 2 ) : 1,  ( 13, 1 ) : 4,  ( 13, 2 ) : 3,  ( 14, 1 ) : 4,  ( 14, 2 ) : 6,
                         ( 15, 1 ) : 4,  ( 15, 2 ) : 7,  ( 16, 1 ) : 5,  ( 16, 2 ) : 2,  ( 17, 1 ) : 5,  ( 17, 2 ) : 3,  ( 18, 1 ) : 5,
                         ( 18, 2 ) : 6,  ( 19, 1 ) : 5,  ( 19, 2 ) : 8,  ( 20, 1 ) : 6,  ( 20, 2 ) : 2,  ( 21, 1 ) : 6,  ( 21, 2 ) : 3,
                         ( 22, 1 ) : 6,  ( 22, 2 ) : 4,  ( 23, 1 ) : 6,  ( 23, 2 ) : 5,  ( 24, 1 ) : 6,  ( 24, 2 ) : 7,  ( 25, 1 ) : 6,
                         ( 25, 2 ) : 8,  ( 26, 1 ) : 7,  ( 26, 2 ) : 3,  ( 27, 1 ) : 7,  ( 27, 2 ) : 4,  ( 28, 1 ) : 7,  ( 28, 2 ) : 6,
                         ( 29, 1 ) : 7,  ( 29, 2 ) : 8,  ( 30, 1 ) : 8,  ( 30, 2 ) : 5,  ( 31, 1 ) : 8,  ( 31, 2 ) : 6,  ( 32, 1 ) : 8,
                         ( 32, 2 ) : 7 } ;

    SET:
       identifier   :  NSet
       subset of    :  Integers
       index        :  i
       definition   :  {1..n} ;

    SET:
       identifier   :  MSet
       subset of    :  Integers
       index        :  j
       definition   :  {1..m} ;

    SET:
       identifier   :  KSet
       subset of    :  Integers
       index        :  k
       definition   :  {1..2} ;

    VARIABLE:
       identifier   :  x
       index domain :  i
       range        :  {1..n} ;

    CONSTRAINT:
       identifier   :  alldiffX
       definition   :  cp::AllDifferent(i, x(i)) ;

    CONSTRAINT:
       identifier   :  AbsX
       index domain :  j
       definition   :  abs(x(graph(j,1)) - x(graph(j,2))) > 1 ;

    CONSTRAINT:
       identifier   :  Symm1
       definition   :  x(1) < x(n) ;

  ENDSECTION  ;

  PROCEDURE
    identifier :  MainInitialization

  ENDPROCEDURE  ;

  PROCEDURE
    identifier :  MainExecution
    body       :  
      ShowProgressWindow;
      solve PlaceNumberPuzzlePlan;
      
      if (PlaceNumberPuzzlePlan.ProgramStatus <> 'Optimal') then
         empty x
      endif;
      
      DialogMessage(GMP::Solution::Count('PlaceNumberPuzzlePlan'));

  ENDPROCEDURE  ;

  PROCEDURE
    identifier :  MainTermination
    body       :  
      return DataManagementExit();

  ENDPROCEDURE  ;

ENDMODEL Main_PlaceNumberPuzzle ;
