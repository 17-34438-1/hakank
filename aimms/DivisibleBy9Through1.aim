
MAIN MODEL DivisibleBy9Through1

  DECLARATION SECTION 

    PARAMETER:
       identifier   :  BBase
       initial data :  10 ;

    PARAMETER:
       identifier   :  N
       definition   :  BBase-1 ;

    PARAMETER:
       identifier   :  M
       definition   :  Power(BBase, N)-1 ;

    SET:
       identifier   :  ii
       subset of    :  Integers
       indices      :  i, j
       definition   :  {1..N} ;

    VARIABLE:
       identifier   :  x
       index domain :  (i)
       range        :  {1..N} ;

    VARIABLE:
       identifier   :  t
       index domain :  (i)
       range        :  {0..M}
       comment      :  "The found numbers. t(1) is the answer to the problem." ;

    CONSTRAINT:
       identifier   :  AllDifferentX
       definition   :  cp::AllDifferent(i, x(i)) ;

    CONSTRAINT:
       identifier   :  ToNum
       index domain :  (i)
       definition   :  t(i) = Sum[j|j<=((N+1)-i), Power(BBase, ((N+1)-i)-j)*x(j)]
       comment      :  "Convert the number in t(i) to x(1..BBase-i)." ;

    CONSTRAINT:
       identifier   :  divisable
       index domain :  (i)
       definition   :  mod( t(N+1-i), i ) = 0 ;

    MATHEMATICAL PROGRAM:
       identifier   :  DivisibleBy9Thru1
       direction    :  minimize
       constraints  :  AllConstraints
       variables    :  AllVariables
       type         :  Automatic ;

  ENDSECTION  ;

  PROCEDURE
    identifier :  MainInitialization

  ENDPROCEDURE  ;

  PROCEDURE
    identifier :  MainExecution
    body       :  
      ShowProgressWindow ;
      solve DivisibleBy9Thru1 where time_limit := 60 /* seconds */ ;
      write AllVariables to file "DivisibleBy9Through1.wrt" ;
      
      DialogMessage(GMP::Solution::Count('DivisibleBy9Thru1'));

  ENDPROCEDURE  ;

  PROCEDURE
    identifier :  MainTermination
    body       :  
      if ( CaseSaveAll( confirm:2 ) = 1 ) then
          return 1;
      else
          return 0;
      endif ;

  ENDPROCEDURE  ;

ENDMODEL DivisibleBy9Through1 ;
