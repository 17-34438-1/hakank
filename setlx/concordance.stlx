//
// Concordance in SetlX.
//
// From http://setl.org/setl/doc/setl.html
//
// This SETL program was created by Hakan Kjellerstrand (hakank@bonetmail.com)
// Also see my SETL page: http://www.hakank.org/setl/
//

load("sort_utils.stlx");
load("string_utils.stlx");

file := "concordance.stlx";

if (#params > 0) {
  file := params[1];
}
lines := readFile(file);

count := {}; // empty mapping
for (line in lines) {
  for (word in split(line, "\\s+")) {
    if (count[word] == om) {
      count[word] := 1;
    } else {
      count[word] += 1;
    }
  }
}

// qsort(list, col) is in sort_utils.stlx

// count_a := qsort_c([c : c in count], 1);          // sort on words
// count_a := reverse(qsort_c([c : c in count], 1)); // reverse sort on words
// count_a := qsort_c([c : c in count], 2);          // sort on counts
count_a := reverse(qsort_c([c : c in count], 2));    // reverse sort on counts


// lpad(str, n) is in string_utils.stlx
for ([word, freq] in count_a) {
  print("$lpad(str(freq),3)$  $freq*\"*\"$ $word$");   // print freq stars and the word
}
