#!/usr/bin/env setl
-- 
-- Project Euler problem 42 in SETL
--
-- """
-- The nth term of the sequence of triangle numbers is given by, 
--     tn = 1/2*n*(n+1); 
-- so the first ten triangle numbers are:
--
-- 1, 3, 6, 10, 15, 21, 28, 36, 45, 55, ...
--
-- By converting each letter in a word to a number corresponding to its 
-- alphabetical position and adding these values we form a word value. For example, 
-- the word value for SKY is 19 + 11 + 25 = 55 = t10. If the word value 
-- is a triangle number then we shall call the word a triangle word.

-- Using words.txt (right click and 'Save Link/Target As...'), a 16K text file 
-- containing nearly two-thousand common English words, how many 
-- are triangle words?
-- """
--
-- This SETL program was created by Hakan Kjellerstrand (hakank@bonetmail.com)
-- Also see my SETL page: http://www.hakank.org/setl/
--

problem42();

proc triangle_number(n);
    return((n*(n+1))/2);
end proc;

proc get_score(name);
    total := 0;
    for i in [1..#name] loop
        total := total+(abs(name(i))-64);
    end loop;

    return total;

end proc;

proc problem42;

    names := getfile("words.txt");
    gsub(names, '"', "");
    names := split(names, ",");

    -- The 20 first triangle numbers
    t20 := [triangle_number(i) : i in [1..20]];
    sum := +/[1 : w in names | get_score(w) in t20];

    print(sum);

end proc;





