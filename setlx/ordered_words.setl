#!/usr/bin/env setl
-- 
-- Ordered words in SETL.
--
-- http://rosettacode.org/wiki/Ordered_words
-- """
-- Define an ordered word as a word in which the letters of the word appear in 
-- alphabetic order. Examples include 'abbey' and 'dirt'.
--
-- The task is to find and display all the ordered words in this dictionary that have the 
-- longest word length. (Examples that access the dictionary file locally assume that you 
-- have downloaded this [unixdict.txt] file yourself.) The display needs to be shown 
-- on this page. 
-- """
--
-- This SETL program was created by Hakan Kjellerstrand (hakank@bonetmail.com)
-- Also see my SETL page: http://www.hakank.org/setl/
--

h := open('unixdict.txt', "r");

words := {};

while not eof(h) loop
  geta(h, word);
  if word /= om and word /= "" and word = insertion_sort(word) then
    words +:= {word};
  end if;
end loop;

max_len := max/[#w : w in words];
print([w : w in words where #w = max_len]);

procedure insertion_sort(A);
   for i in [2..#A] loop
      v := A(i);
      j := i-1;
      while j >= 1 and A(j) > v loop
        A(j+1) := A(j);
        j := j - 1;
      end loop;
      A(j+1) := v; 
   end loop;
   return A;
end procedure;

