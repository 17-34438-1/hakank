#!/usr/bin/env setl
-- 
-- Project Euler problem 21 in SETL
-- 
-- Problem 21
-- """
-- Let d(n) be defined as the sum of proper divisors of n (numbers less 
-- than n which divide evenly into n).
-- If d(a) = b and d(b) = a, where a /= b, then a and b are an amicable 
-- pair and each of a and b are called amicable numbers.
-- 
-- For example, the proper divisors of 220 are 
-- 1, 2, 4, 5, 10, 11, 20, 22, 44, 55 and 110; therefore d(220) = 284. 
-- The proper divisors of 284 are 1, 2, 4, 71 and 142; so d(284) = 220.
-- 
-- Evaluate the sum of all the amicable numbers under 10000.
-- """
--
-- This SETL program was created by Hakan Kjellerstrand (hakank@bonetmail.com)
-- Also see my SETL page: http://www.hakank.org/setl/
--

-- This takes about 17 seconds
problem21();

proc divisors(n);
  return [d : d in [1..1+fix(n/2)] | n mod d = 0];
end proc;

proc problem21;
    nprint("Problem 21: ");
    s := {};
    for a in [1..10000-1] loop
       b := +/divisors(a);
       c := +/divisors(b);
       if a /= b and a = c then
          s +:= {a,b};
       end if;
    end loop;
   print(+/s);
end proc;
