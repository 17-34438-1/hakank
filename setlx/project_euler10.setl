#!/usr/bin/env setl
-- 
-- Project Euler problem 10 in SETL
--
--
-- Problem 10
-- """ 
-- The sum of the primes below 10 is 2 + 3 + 5 + 7 = 17.
-- 
-- Find the sum of all the primes below two million.
-- """
--
-- This SETL program was created by Hakan Kjellerstrand (hakank@bonetmail.com)
-- Also see my SETL page: http://www.hakank.org/setl/
--

problem10();
--problem10b();

-- This version takes about 4.5 seconds.
proc problem10;
   nprint("Problem 10: ");
    n := 2000000;
    primes := [1 .. n];
    for i in [2 .. n] loop
        if primes(i) = 0 then
            continue;
        end if;
        j := 2;
        while i * j <= n loop
            primes(i * j) := 0;
            j := j + 1;
        end loop;
    end loop;

    print(+/primes(2..n));

end proc;


-- This version takes about 40 seconds...
proc problem10a();
    nprint("Problem 10: ");
    -- n := 2000000;
       n := 2000000;
    res := 2;
    primes := [2];
    i := 3;
    while i < n loop
        if is_prime(i) then
        -- if is_prime2(i,primes) then
            -- primes with:= i;
            res +:= i;
        end if;
        i +:= 2;
    end loop;

   print(res);

end proc;


-- Using sets
-- Takes about 14.9s (and > 3.5Gb)
proc problem10b();
    nprint("Problem 10: ");
    -- n := 2000000;
       n := 2000000;
    primes := {2..n};
    twos := {2*i : i in {2..fix(n/2)}};
    primes -:= twos;
    i := 3;
    --p_max := max/primes;
    while i < n/i loop
        x := {i*k : k in {i..fix(n/i)}};        
        primes -:= x;
        -- p_max := max/primes;
        i +:= 2;
    end loop;

   print(+/primes);

end proc;
