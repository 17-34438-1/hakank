#!/usr/bin/env setl
-- 
-- Project Euler problem 22 in SETL
-- 
-- Problem 22
--
-- """
-- Using names.txt (right click and 'Save Link/Target As...'), a 46K 
-- text file containing over five-thousand first names, begin by sorting 
-- it into alphabetical order. Then working out the alphabetical value 
-- for each name, multiply this value by its alphabetical position in the 
-- list to obtain a name score.
--
-- For example, when the list is sorted into alphabetical order, COLIN, 
-- which is worth 3 + 15 + 12 + 9 + 14 = 53, is the 938th name in 
-- the list. So, COLIN would obtain a score of 938 53 = 49714.
--
-- What is the total of all the name scores in the file?")
-- """
--
-- This SETL program was created by Hakan Kjellerstrand (hakank@bonetmail.com)
-- Also see my SETL page: http://www.hakank.org/setl/
--

problem22();

proc problem22;
   nprint("Problem 22: ");
   names := getfile("euler_names.txt"); 
   gsub(names, '"', "");
   names := qsort(split(names,","));
   ix := 1;
   total := 0;
   for n in names loop
     total +:= ix * (+/[abs(m)-64 : m in str(n)]);
     ix +:= 1;
   end loop;
   print(total);

end proc;

--
-- I'm surprised that (GNU) SETL don't have a built-in 
-- sort function. This is borrowed from Rosetta Code
-- http://rosettacode.org/wiki/Sorting_algorithms/Quicksort#SETL
--
proc qsort(a);
  if #a > 1 then
    pivot := a(#a div 2 + 1);
    a := qsort([x in a | x < pivot]) +
         [x in a | x = pivot] +
         qsort([x in a | x > pivot]);
  end if;
  return a;
end proc;
