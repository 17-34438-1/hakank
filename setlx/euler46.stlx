// 
// Project Euler problem 46 in SETL
//
// """  
// It was proposed by Christian Goldbach that every odd composite number can be 
// written as the sum of a prime and twice a square.
//
// 9 = 7 + 2×1^2
// 15 = 7 + 2×2^2
// 21 = 3 + 2×3^2
// 25 = 7 + 2×3^2
// 27 = 19 + 2×2^2
// 33 = 31 + 2×1^2
//
// It turns out that the conjecture was false.
//
// What is the smallest odd composite that cannot be written as the 
// sum of a prime and twice a square?
// """
//
// This SETL program was created by Hakan Kjellerstrand (hakank@bonetmail.com)
// Also see my SETL page: http://www.hakank.org/setl/
//



problem46 := procedure() {

  res := 0;
  for (i in [3,5..10000]) {
    if (isPrime(i)) { continue; }
    s := round(sqrt(i/2));
    found := false;
    for (j in [1..s]) {
      ts := j*j*2;
      if (isPrime(abs(i-ts))) {
        found := true;
        break;
      }
           
    }
        
    if (!found) {
      res := i;
      break;
    }
  }

  print(res);

};

problem46();
