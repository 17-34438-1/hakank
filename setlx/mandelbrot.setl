#!/usr/bin/env setl
-- 
-- Mandelbrot set in SETL
--
-- See 
--  * http://en.wikipedia.org/w/index.php?title=Mandelbrot_set
--  * http://rosettacode.org/wiki/Mandelbrot_set
--
-- This solution was inspired by the Icon/Unicon version:
--   http://rosettacode.org/wiki/Mandelbrot_set#Icon_and_Unicon
--
--
-- This SETL program was created by Hakan Kjellerstrand (hakank@bonetmail.com)
-- Also see my SETL page: http://www.hakank.org/setl/
--

width := 90;
height := 50;
limit := 50;
for y in [1..height] loop
  for x in [1..width] loop 
    z:=xcomplex(0,0);
    c:=xcomplex(2.5*x/width-2.0,(2.0*y/height-1.0));
    j:=0;
    while j<limit and cAbs(z)<2.0 loop
       z := cAdd(cMul(z,z),c);
       j+:= 1;
    end loop;
    p := " ";
    if j=limit then p := "#"; end if;
    nprint(p);
  end loop;
  print();
end loop;


procedure xcomplex(r,i);
  return [r,i];
end procedure;

procedure cAdd(x,y);
    return xcomplex(x(1)+y(1),x(2)+y(2));
end procedure;
 
procedure cMul(x,y);
    return xcomplex(x(1)*y(1)-x(2)*y(2),x(1)*y(2)+x(2)*y(1));
end procedure;
 
procedure cAbs(x);
    return sqrt(x(1)*x(1)+x(2)*x(2));
end procedure;
 
 
