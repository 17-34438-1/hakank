// 
// Pangram checker in SETL
//
// http://rosettacode.org/wiki/Pangram_checker
// """
// Write a function or method to check a sentence to see if it is a 
// pangram or not and show its use.
// 
// A pangram is a sentence that contains all the letters of the 
// English alphabet at least once, for example: The quick brown fox jumps 
// over the lazy dog. 
// """
//
// This SETL program was created by Hakan Kjellerstrand (hakank@bonetmail.com)
// Also see my SETL page: http://www.hakank.org/setl/
//

var alpha;
alpha := "abcdefghijklmnopqrstuvwxyz";

is_pangram := procedure(s) {
  return forall(c in alpha | c in toLowerCase(s));
};


// another version
is_pangram1 := procedure(s) {
  chars := {c : c in toLowerCase(s)};
  for (c in alpha) {
    if (!(c in chars)) {
      return false;
    }
  }
  return true;
};

// another version
is_pangram2 := procedure(s) {
  // convert to a set
  chars := {c : c in toLowerCase(s) | c in alpha};
  return #chars == 26;
};



s1 := "The quick brown fox jumps over the lazy dog";
print(s1, ": " , is_pangram(s1));
print(s1, ": ", is_pangram1(s1));
print(s1, ": ", is_pangram2(s1));

s2 := "The fox jumps over the lazy dog";
print(s2, ": ", is_pangram(s2));
print(s2, ": ", is_pangram1(s2));
print(s2, ": ", is_pangram2(s2));

