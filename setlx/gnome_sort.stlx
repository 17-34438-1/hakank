// 
// Gnome sort in SetlX.
//
// See 
//  * http://en.wikipedia.org/wiki/Gnome_sort
//  * http://rosettacode.org/wiki/Sorting_algorithms/Gnome_sort
//
//
// This SetlX program was created by Hakan Kjellerstrand (hakank@bonetmail.com)
// Also see my SETL page: http://www.hakank.org/setl/
//

gnome_sort := procedure(rw a) {
  size := #a;
  i := 2;
  j := 3;
  while (i <= size) {
    if (a[i-1] <= a[i]) {
      // for descending sort, use >= for comparison
      i := j;
      j += 1;
    } else {
      [a[i-1],a[i]] := [a[i],a[i-1]];
      i -= 1;
      if (i == 1) {
        i := j;
        j += 1;
      }
    }
  }
};


lst := [23, 76, 99, 58, 97, 57, 35, 89, 51, 38, 95, 92, 24, 46, 31, 24, 14, 12, 57, 78];
print(lst);
gnome_sort(lst);
print(lst);

time0 := now();
rand_range := [1..10000];
lst:=[rnd(rand_range) : i in [1..1000]];
gnome_sort(lst);
print("It took ", (now()-time0)/1000.0," seconds to sort ", #lst, " numbers");
