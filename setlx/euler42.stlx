// 
// Project Euler problem 42 in SETL
//
// """
// The nth term of the sequence of triangle numbers is given by, 
//     tn = 1/2*n*(n+1); 
// so the first ten triangle numbers are:
//
// 1, 3, 6, 10, 15, 21, 28, 36, 45, 55, ...
//
// By converting each letter in a word to a number corresponding to its 
// alphabetical position and adding these values we form a word value. For example, 
// the word value for SKY is 19 + 11 + 25 = 55 = t10. If the word value 
// is a triangle number then we shall call the word a triangle word.

// Using words.txt (right click and 'Save Link/Target As...'), a 16K text file 
// containing nearly two-thousand common English words, how many 
// are triangle words?
// """
//
// This SETL program was created by Hakan Kjellerstrand (hakank@bonetmail.com)
// Also see my SETL page: http://www.hakank.org/setl/
//

triangle_number := procedure (n) {
    return((n*(n+1))/2);
};

get_score := procedure(name) {
    total := 0;
    for (i in [1..#name]) {
        total := total+(abs(name[i])-64);
    }

    return total;

};

// strip the string s using chars c
strip_chars := procedure(s,c) {
   s2 := "";
   for (p in [1..#s]) {
     if (s[p] notin c) {
        s2 += s[p];
     }
   }
   return s2;
};

// split the string s using the chars c
split_on_char := procedure(s, c) {
   s2 := [];
   last_pos := 1;
   for (pos in [1..#s]) {
      if (s[pos] in c) {
         v := s[last_pos..pos-1];
         s2 += [v];
         last_pos := pos+1;
      }     
   }
   last := s[last_pos..#s];
   s2 += [last];
   return s2;
};


problem42 := procedure() {

    names := readFile("words.txt");
    names := strip_chars(names[1], "\"");
    names := split_on_char(names, ",");

    // The 20 first triangle numbers
    t20 := [triangle_number(i) : i in [1..20]];
    sum := +/[1 : w in names | get_score(w) in t20];

    print(sum);

};


problem42();