// 
// Project Euler problem 41 in SETL
//
// """
// We shall say that an n-digit number is pandigital if it makes use of all 
// the digits 1 to n exactly once. For example, 2143 is a 4-digit pandigital 
// and is also prime.
//
// What is the largest n-digit pandigital prime that exists?
// """
//
// This SETL program was created by Hakan Kjellerstrand (hakank@bonetmail.com)
// Also see my SETL page: http://www.hakank.org/setl/
//

problem41 := procedure() {
  // Simplification (from one of the answers)
  // n=9 is not possible since 1+2+3+4+5+6+7+8+9=45 is divisible by 3
  // n=8 is not possible since 1+2+3+4+5+6+7+8=36 is divisible by 3
  n := 7;
  m := 0;
  while (m == 0 && n >= 4) {
    p := [];
    for (i in [n,n-1..1]) {
      p := p + [i];
    }

    // note: it's reversed sorted so we stop at first prime
    // perms := perms2(p);
    perms := reverse(perms3(p));
    for (p in perms) {
      v := int(join(p,""));
      if (isPrime(v)) {
        m := v; // found it
        break;
      }
      
    }

    n -= 1;
    
  }
  
  print(m);

};

// Permutation (exhaustive)
perms2 := procedure(s) {
  if (s == []) {
    return [[]];
  } else {
    return [ [a]+b : a in s, b in perms2([ p in s | p != a]) ];
  }

};


perms3 := procedure(s) {

  return [p : p in permutations(s)];

};


problem41();