#!/usr/bin/env setl
-- 
-- Project Euler problem 3 in SETL
--
-- Problem 3
-- """
-- The prime factors of 13195 are 5, 7, 13 and 29.
-- What is the largest prime factor of the number 600851475143 ?
-- """
--
-- This SETL program was created by Hakan Kjellerstrand (hakank@bonetmail.com)
-- Also see my SETL page: http://www.hakank.org/setl/
--

problem3();

proc problem3;
  nprint("Problem 3: ");
  n := 600851475143;
  f := fix(sqrt(n));
  -- fact := factors(n);
  fact := prime_factors(n);
  print(fact(#fact));
end proc;

procedure prime_factors(n); 
   facts := []; 
   while even(fix(n)) loop facts with:= 2; n /:= 2; end loop;
   while exists k in [3,5..ceil(sqrt(float(n)))] | fix(n) mod k = 0 loop
      facts with:= k; 
      n /:= k;
   end loop;
   facts with:= n;
   return facts;
end prime_factors;

-- Elegant but much slower
proc factors(n);
  r := [f : f in [2..fix(sqrt(n))] | n mod f = 0 
               and 
               (forall m in [2..fix(sqrt(f))] | f mod m > 0) ];
  return(r);
end proc;
