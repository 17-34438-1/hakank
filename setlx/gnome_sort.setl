#!/usr/bin/env setl
-- 
-- Gnome sort in SETL.
--
-- See 
--  * http://en.wikipedia.org/wiki/Gnome_sort
--  * http://rosettacode.org/wiki/Sorting_algorithms/Gnome_sort
--
--
-- This SETL program was created by Hakan Kjellerstrand (hakank@bonetmail.com)
-- Also see my SETL page: http://www.hakank.org/setl/
--

lst := [23, 76, 99, 58, 97, 57, 35, 89, 51, 38, 95, 92, 24, 46, 31, 24, 14, 12, 57, 78];
print(lst);
gnome_sort(lst);
print(lst);

setrandom(0);
time0 := time();
lst:=[random 10000 : i in [1..1000]];
gnome_sort(lst);
print("It took", (time()-time0)/1000,"seconds to sort", #lst, "numbers");


procedure gnome_sort(rw a);
  size := #a;
  i := 2;
  j := 3;
  while i <= size loop
    if a(i-1) <= a(i) then
       -- for descending sort, use >= for comparison
       i := j;
       j +:= 1;
    else
      [a(i-1),a(i)] := [a(i),a(i-1)];
      i -:= 1;
      if i = 1 then
        i := j;
        j +:= 1;
      end if;
    end if;
  end loop;
end procedure;
