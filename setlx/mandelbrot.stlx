// 
// Mandelbrot set in SETL
//
// See 
//  * http://en.wikipedia.org/w/index.php?title=Mandelbrot_set
//  * http://rosettacode.org/wiki/Mandelbrot_set
//
// This solution was inspired by the Icon/Unicon version:
//   http://rosettacode.org/wiki/Mandelbrot_set#Icon_and_Unicon
//
//
// This SETL program was created by Hakan Kjellerstrand (hakank@bonetmail.com)
// Also see my SETL page: http://www.hakank.org/setl/
//

// SetlX don't have support for complex numbers (what I know)
xcomplex := procedure(r,i) {
  return [r,i];
};

cAdd := procedure(x,y) {
    return xcomplex(x[1]+y[1],x[2]+y[2]);
};
 
cMul := procedure(x,y) {
    return xcomplex(x[1]*y[1]-x[2]*y[2],x[1]*y[2]+x[2]*y[1]);
};
 
cAbs := procedure(x) {
    return sqrt(x[1]*x[1]+x[2]*x[2]);
};
 
t1:=now();
width := 90;
height := 50;
limit := 50;
for (y in [1..height]) {
  p:="";
  for (x in [1..width]) {
    z:=xcomplex(0,0);
    c:=xcomplex(2.5*x/width-2.0,2.0*y/height-1.0);
    j:=0;
    while (j < limit && cAbs(z)<2.0) {
      z := cAdd(cMul(z,z),c);
      j += 1;
    }
    if (j == limit) { 
      p += "#"; 
    } else { 
      p+= " "; 
    }
  }
  print(p);
}

t2:=now();
print("It took $(t2-t1)/1000.0$s");