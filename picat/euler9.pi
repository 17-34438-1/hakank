/* 

  Euler #9 in Picat.

  Problem 9
  """
  A Pythagorean triplet is a set of three natural numbers, a  b  c, for which,
  a^2 + b^2 = c^2

  For example, 3^2 + 4^2 = 9 + 16 = 25 = 5^2.

  There exists exactly one Pythagorean triplet for which a + b + c = 1000.
  Find the product abc.
  """

  This Picat model was created by Hakan Kjellerstrand, hakank@gmail.com
  See also my Picat page: http://www.hakank.org/picat/

*/

import cp.

main => go.

go => euler9.


triplet(A,B,C) = Prod =>
    LD = [A,B,C],
    LD :: 1..500,
    A + B + C #= 1000,
    A #=< B, % symmetry breaking
    B #=< C, 
    Prod #= A * B *C,
    A**2 + B**2 - C**2 #= 0,
    solve([degree,split], LD).

euler9 =>
       Prod = triplet(_A,_B,_C),
       % writeln([A,B,C]),
       writeln(Prod).

