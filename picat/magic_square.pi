/*

  Magic squares in Picat.


  Model created by Hakan Kjellerstrand, hakank@gmail.com
  See also my Picat page: http://www.hakank.org/picat/

*/

import util.
import cp.

main => go.


%
% Simple run
%
go =>
        magic(5,_Square).


%
% Run for different sizes
% 
% ff/updown solves N=6 in 0.06s and 6856 backtracks
%                   10 in 2.99s and 289939 backtracks
% ffd/updown solves N=10 in 2.37s and 306336 backtracks
go2 =>
        foreach(N in 3..11) time2($magic(N,_Square)) end.


%
% All solutions.
%
go3 =>
        N = 4,
        L = findall(Square,magic(N,Square)),
        Len = length(L),
        writeln("Len: %d\n",Len).


magic(N,Square) =>

        writef("\n\nN: %d\n", N),
        NN = N*N,
        Sum = N*(NN+1)//2,% magical sum
        writef("Sum = %d\n", Sum),

        Square = new_array(N,N),
        Square in 1..NN,

        all_different(Square),

        foreach(I in 1..N)
             Sum #= sum([ T : J in 1..N, T = Square[I,J]]),% rows
             Sum #= sum([ T : J in 1..N, T = Square[J,I]]) % column
        end,

        % diagonal sums
        Sum #= sum([Square[I,I] : I in 1..N]),
        Sum #= sum([Square[I,N-I+1] : I in 1..N]),

        % Symmetry breaking
        Square[1,1] #< Square[1,N],
        Square[1,1] #< Square[N,N],
        Square[1,1] #< Square[N,1],
        Square[1,N] #< Square[N,1],


        solve([ffd,updown],Square),

        print_square(Square).

% Using rows(), columns(), diagonal1(), and diagonal2() from the util module
magic2(N,Square) =>

        writef("\n\nN: %d\n", N),
        NN = N*N,
        Sum = N*(NN+1)//2,% magical sum
        writef("Sum = %d\n", Sum),

        Square = new_array(N,N),
        Square in 1..NN,

        all_different(Square),

        foreach(Row in Square.rows()) Sum #= sum(Rows) end,
        foreach(Column in Square.columns()) Sum #= sum(Column) end,

        % diagonal sums
        Sum #= sum(Square.diagonal1()),
        Sum #= sum(Square.diagonal2()),

        % Symmetry breaking
        Square[1,1] #< Square[1,N],
        Square[1,1] #< Square[N,N],
        Square[1,1] #< Square[N,1],
        Square[1,N] #< Square[N,1],


        solve([ffd,updown],Square),

        print_square(Square).




print_square(Square) =>
        N = Square.length,
        foreach(I in 1..N)
           foreach(J in 1..N)
               writef("%3d ", Square[I,J])
           end,
           writef("\n")
        end,
        writef("\n").

