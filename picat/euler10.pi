/* 

  Euler #10 in Picat.

  Problem 10
  """ 
  The sum of the primes below 10 is 2 + 3 + 5 + 7 = 17.
  
  Find the sum of all the primes below two million.
  """

  This Picat model was created by Hakan Kjellerstrand, hakank@gmail.com
  See also my Picat page: http://www.hakank.org/picat/

*/

main => go.

go => euler10.


euler10 => 
   writeln(sum(sieve(2000000))).

sieve(N) = Res => 
   Sieve = new_array(N),
   foreach(I in 2..round(sqrt(N)), J in I*I..I..N)
      Sieve[J] := 1
   end,
   Res := [I : I in 2..N, var(Sieve[I])].



% slower
euler10b =>
       Sum = 2,
       foreach(I in 3..2..2000000)
            if prime(I) then
                Sum := Sum + I
            end
       end,
       writeln(Sum).


% using list comprehension insead
% slower
euler10c =>
       writeln(sum([I : I in 1..2000000, prime(I)])).

% slower
euler10d =>
       Sum = 2, % for 2
       I = 3,
       while(I <= 2000000)
          if prime(I) then
              Sum := Sum + I
          end,
          I := I + 2
        end,
        writeln(Sum).

