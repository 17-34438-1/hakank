/* 

  Bananas problem in Picat.

  """
  In three dollars, you get 5 bananas, in five dollars, 7 oranges, in
  seven dollars, 9 mangoes and in nine dollars, three apples, I need to
  purchase 100 fruits in 100 dollars. Please keep in mind that all type
  of fruits need to be purchased but I do not like banana and apple, so
  these should be of minimum quantity.
  """

  This Picat model was created by Hakan Kjellerstrand, hakank@gmail.com
  See also my Picat page: http://www.hakank.org/picat/

*/


% import util.
import cp.


main => go.


go =>

   X = [Bananas, Oranges,Mangoes,Apples],
   X in 1..100,

   TheSum #= Bananas+Apples,

   % (5*Bananas)/3 + (7*Oranges)/5 + (9*Mangoes)/7 + (3*Apples)/9 #= 100,
   % (common denimator: 3*5*7*9 = 945)
   (5*315*Bananas) + (7*189*Oranges) + (9*135*Mangoes) + (3*105*Apples) #= 100*945,

   sum(X) #= 100,

   solve($[min(TheSum)], X),

   writeln(theSum=TheSum),
   writeln(x=X),
   
   nl.
