/*

  Five elements problem in Picat.

  From 
  Charles W. Trigg, PI MU EPSILON JOURNAL, Valume 6, Fall 1977, Number 5
  """
  From the following square array of the first 25 positive integers, 
  choose five, no two of the same row or column, so that the maximum of
  the five elements is as small as possible.

     2 13 16 11 23
    15  1  9  7 10
    14 12 21 24  8
     3 25 22 18  4
    20 19  6  5 17
  """

  Model created by Hakan Kjellerstrand, hakank@gmail.com
  See also my Picat page: http://www.hakank.org/picat/

*/

import cp.

main => go.

go =>

   Matrix = 
       [[ 2, 13, 16, 11, 23],
        [15,  1,  9,  7, 10],
        [14, 12, 21, 24,  8],
        [ 3, 25, 22, 18,  4],
        [20, 19,  6,  5, 17]],
   N = 5,

   %
   % decision variables
   %
   X = new_array(N,N),
   XVars = vars(X),
   XVars in 0..1,

   Y = new_list(N),
   Y in 1..N*N,

   %
   % constraints
   %

   % ensure unicity of rows and columns
   foreach(I in 1..N)
      sum([X[I,J] : J in 1..N]) #= 1,
      sum([X[J,I] : J in 1..N]) #= 1
   end,

   % Find the specific row I for which X[I,J] is 1.
   %
   % Using freeze/2 and using the Is list in the labeling 
   % seems to be the only way (when using this matrix approach)
   Is = [],
   foreach(J in 1..N)
      I in 1..N,

      freeze(I, 1 #= X[I,J]),
      freeze(I, Y[J] #= Matrix[I,J]),

      % matrix_element(X,I,J,1),
      % Y[J] #= Matrix[I,J],

      % It would be nice is we can do simply this,
      % i.e. without the freeze:
      % 1 #= X[I,J],
      % Y[J] #= Matrix[I,J],

      Is := Is ++ [I]

    end,

   MaxY #= max(Y),

   Vars = XVars ++ Y ++ Is,

   solve([$min(MaxY), ff], Vars),
   % solve(Vars),

   writeln(y=Y),
   writeln(maxY=MaxY),
   foreach(Row in X) writeln(Row) end,
   nl.

%
% Val = X[I,J] 
%
% matrix_element(X, I, J, Val) =>
%   nth(I, X, Row),
%   element(J, Row, Val).

matrix_element(X, I, J, Val) =>
   freeze(I, (element(I, X, Row),freeze(J,element(J,Row,Val)))).
