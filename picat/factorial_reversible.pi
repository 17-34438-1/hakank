/* 

  Reversible version of factorial in Picat.

  This program show how to do a reversible version of factorial(N,F)
  i.e. where both N and F can be given as var's.

  This Picat model was created by Hakan Kjellerstrand, hakank@gmail.com
  See also my Picat page: http://www.hakank.org/picat/

*/


% import util.
import cp.


main => go.


go => 
   factorial(7,F7),
   println(f7=F7),
   factorial(N5040,5040),
   println(n5040=N5040),

   factorial(17,F17),
   writeln(f17=F17),
   factorial(N17,F17),
   println(n17=N17),

   nl.

% some harder cases
go2 =>
   foreach(I in 1..3) 
     N = 10**I,
     println(n=N),
     time(factorial(N,F1)),
     writeln(f1=F1),
     % and back
     time(factorial(N2,F1)),
     println(n2=N2),
     nl
   end,
   nl.

  
%
% This version of factorial/2 is reversible
% once/1 added so we don't get into infinite loop
% if/when backtracking.
%
table
factorial(N,F) =>
  once(factorial1(N,F)).

% note: if stated as fact(0,F) it don't work
% this should _not_ be tabled
factorial1(N,F) ?=> 
  N = 0,
  F = 1.
factorial1(N,F) =>
  factorial1(N1,F1),
  N = N1 + 1,
  F = N * F1.

