/* 

  Block height problem in Picat.

  From 
  https://www.cs.bham.ac.uk/research/projects/poplog/doc/popprimer.dir/node195.html
  """
   3. Try using the problem solver to solve the problem of selecting some blocks 
      from a pile of blocks to build a tower of exactly a given height. E.g. you may 
      be given a list of numbers representing the heights of the available blocks:

      [3 16 12 22 5 5 24 14 8 7 22 11]

      and the task of finding blocks to make a tower exactly 30 units high.
  """

  This Picat model was created by Hakan Kjellerstrand, hakank@gmail.com
  See also my Picat page: http://www.hakank.org/picat/

*/


% import util.
import planner.
import cp.


main => go.


go =>
  Init = [0|[]],
  available(Available),
  println(available=Available),
  println(init=Init),
  best_plan(Init,Plan),
  foreach(Move in Plan)
    println(Move)
  end,

  nl.

go2 => 
  available(Available),
  final([Final|_]),
  println(available=Available),
  println(final=Final),

  N = Available.length,
  X = new_list(N),
  X :: 0..100,

  sum([X[I] *Available[I] : I in 1..N]) #= Final,
  Z #= sum([X[I] : I in 1..N]),
  solve($[min(Z)], X),
  % solve(X),

  println(x=X),
  println(z=Z),
  println(solution=[[Available[I] : J in 1..X[I]] : I in 1..N, X[I] > 0].flatten()),
  nl,
  % fail,
  nl.

final([S|_L]) => S = 130.


action([Sum1|From1],To,Move,Cost) =>
   % println($action([Sum1|From1],To,Move,Cost) ),
   available(Available),
   member(H,Available), % not member(H,From1),
   Sum2 = Sum1 + H,
   Move = [add,H,to,From1,giving,Sum2],
   To = [Sum2|From1++[H]],
   Cost = 1.


index(-)
available([3,16,12,22,5,5,24,14,8,7,22,11]).

% available(Available) => 
%   Available = [1 + random2() mod 25 : _ in 1..20].remove_dups().



