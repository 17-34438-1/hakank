/*

  xkcd's knapsack/subset-sum problem  in Picat.

  http://xkcd.com/287/

  Some amount (or none) of each dish should be ordered to give a total of exact 15.05

  Model created by Hakan Kjellerstrand, hakank@gmail.com
  See also my Picat page: http://www.hakank.org/picat/

*/

import cp.

main => go.

scalar_product(A, X, Product) => 
   Product #= sum([S : I in 1..A.length, S #= A[I]*X[I]]).

xkcd(X) =>
   Prices = [215, 275, 335, 355, 420, 580],
   Total = 1505,
   Len = Prices.length,
   X = new_list(Len),
   X in 0..10,
   scalar_product(Prices, X, Total),
   solve(X).

go =>
   L = findall(X,xkcd(X)),
   writeln(L).
