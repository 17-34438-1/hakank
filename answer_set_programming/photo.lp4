%
% Photo problem in ASP.
% 
% Problem statement from Mozart/Oz tutorial:
% http://www.mozart-oz.org/home/doc/fdt/node37.html#section.reified.photo
% """
% Betty, Chris, Donald, Fred, Gary, Mary, and Paul want to align in
% one row for taking a photo. Some of them have preferences next
% to whom they want to stand:
%
%    1. Betty wants to stand next to Gary and Mary.
%    2. Chris wants to stand next to Betty and Gary.
%    3. Fred wants to stand next to Mary and Donald.
%    4. Paul wants to stand next to Fred and Donald.
%
% Obviously, it is impossible to satisfy all preferences. Can you find
% an alignment that maximizes the number of satisfied preferences?
% """
% Oz' solution: 
%   6 # alignment(betty:5  chris:6  donald:1  fred:3  gary:7   mary:4   paul:2)
% [5, 6, 1, 3, 7, 4, 2]
% 
% 
% This was created by Hakan Kjellerstrand, hakank@gmail.com
% See also http://www.hakank.org/answer_set_programming/
%

%
% There are 8 optimal solutions with 6 satisfied preferences.
%

% persons(betty;chris;donald;fred;gary;mary;paul).
persons(betty).
persons(chris).
persons(donald).
persons(fred).
persons(gary).
persons(mary).
persons(paul).

positions(1..7).

% Preferences:
% pref(betty, gary;mary).
% pref(chris, betty;gary).
% pref(fred, mary;donald).
% pref(paul, fred;donald).

pref(betty,gary).
pref(betty,mary).
pref(chris,betty).
pref(chris,gary).
pref(fred,mary).
pref(fred,donald).
pref(paul,fred).
pref(paul,donald).


% alldifferent positions
1 { position(Person, Pos) : positions(Pos) } 1 :- persons(Person).
1 { position(Person, Pos) : persons(Person) } 1 :- positions(Pos).

next_to(P1,P2) :-
        pref(P1,P2),
        position(P1,Pos1),
        position(P2,Pos2),
        |Pos1-Pos2| == 1.

total(Tot) :- Tot = { next_to(P1,P2) }.

% maximize the number of satisfied preferences
#maximize { 1,P1,P2:next_to(P1,P2) }.
       
#show position/2.
#show total/1.
#show next_to/2.
