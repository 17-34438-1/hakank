// 
// Hamming numbers in Frink
// 
// From Rosetta code:
// http://rosettacode.org/wiki/Hamming_numbers
// """
// Hamming numbers are numbers of the form
//   H = 2**i * 3**j * 5**k, where  i, j, k >= 0. 
//
// Hamming numbers are also known as ugly numbers and also 5-smooth numbers 
// (numbers whose prime divisors are less or equal to 5).
// 
// Generate the sequence of Hamming numbers, in increasing order. In particular:
//
//  * Show the first twenty Hamming numbers.
//  * Show the 1691st Hamming number (the last one below 231).
//  * Show the one millionth Hamming number (if the language – or a 
//    convenient library – supports arbitrary-precision integers). 
// """
// 
// 
// This Frink program was created by Hakan Kjellerstrand (hakank@bonetmail.com)
// Also see my Frink page: http://www.hakank.org/frink/
//

// Inspired by one of the D solutions.
// But it's quite slow on larger n (e.g. 1000000)
hamming[n] :=
{
  q = new set[1,2,3,5]
  low = 1
  for i = 1 to n
  {
     low = min[q]
     q.remove[low]
     q.put[low * 2]
     q.put[low * 3]
     q.put[low * 5]
  }
  
  return low
}


// Much faster
hamming2[n] :=
{
   h = new array
   [next2,next3,next5] = [2,3,5]
   i = j = k = -1
   m = 0
   while m <= n
   {
      h@m = min[[next2,next3,next5]]
      if h@m == next2
      {
         i = i + 1
         next2 = 2*h@i
      }
    
      if h@m == next3
      {
         j = j + 1
         next3 = 3*h@j
      }
      
      if h@m == next5
      {
         k = k + 1
         next5 = 5*h@k
      }
      m = m + 1
   }

   h.pushFirst[1]
   return h@(n-1)
   
}



// First 20 Hamming numbers:  1 2 3 4 5 6 8 9 10 12 15 16 18 20 24 25 27 30 32 36
// 1691 st Hamming number:  2125764000

for i = 1 to 20
   print[hamming[i] + " "]
println[]

println[hamming[1691]]

// println[hamming[1000000]] // too slow

for i = 1 to 20
   print[hamming2[i] + " "]
println[]
println[hamming2[1691]]

// About 6.4s
t1 = now[]
println[hamming2[1000000]]
println["hamming2[1000000] took " + (now[]-t1 -> "seconds")]

/*
// About 1:06 minutes
t2 = now[]
println[hamming2[10000000]]
println["hamming2[10000000] took " + (now[]-t2 -> "seconds")]
*/