/*

  Get HTTP headers in Frink

  This Frink program was created by Hakan Kjellerstrand (hakank@bonetmail.com)
  Also see my Frink page: http://www.hakank.org/frink/

*/


//
// Get all the HTTP header from an URL
// Returns as a dictionary
//
getHTTPHeaders[url] :=
{
   url = newJava["java.net.URL", url]
   conn = url.openConnection[]

   res = new dict
   i = 0
   while true
   {
      name = conn.getHeaderFieldKey[i]
      value = conn.getHeaderField[i]
      if !name && !value
         break
      
      if !name
         res@("Response code")=value
      else
         res@name = value
      i = i + 1
   }

   return res
   
}

//
// get the Last-Modify header
//
// Last-modified seems to be with like this
// Mon, 10 Sep 2012 06:31:00 GMT
//
lastModified[headers] :=
{
   last_modified = headers@("Last-Modified")
   
   return last_modified
}

printAllHeaders[headers] :=
{
   for [key, value] = headers
      println["$key: $value"]
}

