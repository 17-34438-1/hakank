// 
// Project Euler problem 35 in Frink
//
// Problem 35
// """
// The number, 197, is called a circular prime because all rotations 
// of the digits: 197, 971, and 719, are themselves prime.
//
// There are thirteen such primes below 100: 
// 2, 3, 5, 7, 11, 13, 17, 31, 37, 71, 73, 79, and 97.
//
// How many circular primes are there below one million?
// """
//
// This Frink program was created by Hakan Kjellerstrand (hakank@bonetmail.com)
// Also see my Frink page: http://www.hakank.org/frink/
//

//
// check is a number (a prime) is a circular prime
//
is_circular_prime[p] :=
{
  s = toString[p]
  size = length[s]
  for i = 1 to length[s]
  {
    ss = substr[s, i, size+1] + substr[s, 0, i]
    v = parseInt[ss]
    if !isPrime[v]
      return false
  }
  
  return true

}


primes = select[2 to 1000000-1, {|i| isPrime[i]}]
println[length[select[primes, {|p| is_circular_prime[p]}]]]