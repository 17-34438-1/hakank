// 
// Project Euler problem 32 in Frink
//
// Problem 32
// """
// We shall say that an n-digit number is pandigital if it makes use of 
// all the digits 1 to n exactly once; for example, the 5-digit number, 
// 15234, is 1 through 5 pandigital.
//
// The product 7254 is unusual, as the identity, 39 Ã— 186 = 7254, 
// containing multiplicand, multiplier, and product is 1 through 9 
// pandigital.
//
// Find the sum of all products whose multiplicand/multiplier/product 
// identity can be written as a 1 through 9 pandigital.
// HINT: Some products can be obtained in more than one way so be sure 
// to only include it once in your sum.
// """
//
// This Frink program was created by Hakan Kjellerstrand (hakank@bonetmail.com)
// Also see my Frink page: http://www.hakank.org/frink/
//

ssum = 0
prodhash = new dict
multifor [a,b] = [2 to 98, a+1 to 9876]
{
  prod = a*b
  ll = a + "" + b + "" + prod
  if length[ll] == 9 && !(ll =~ %r/0/)
  {
    hash = toSet[map[{|i| parseInt[i]}, charList[""+ll]]];
    if length[hash] == 9 && prodhash@prod == undef
    {
      // println["a: $a b: $b prod: $prod ll: $ll"]
      ssum = ssum + prod
      prodhash@prod = 1
    }
  }
}

println[ssum]
