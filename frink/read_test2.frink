// 
// Read test in Frink
//
// This is my standard a.*b.*c ... test when learning a new
// programming language.
//
// The goal is to show all words from a dictionary which matches
// the regular expressions (for n = 4):
//   a.*b.*c.*d, b*.c.*d.*e, c.*d.*e.*f etc.
//   
// This Frink program was created by Hakan Kjellerstrand (hakank@bonetmail.com)
// Also see my Frink page: http://www.hakank.org/frink/
//

dict_file = "/usr/share/dict/words"
n = 5
lang = "eng"

println["ARGS: $ARGS"]

if length[ARGS] > 0
   dict_file = ARGS@0

if length[ARGS] > 1
   n = parseInt[ARGS@1]

if length[ARGS] > 2
   lang = ARGS@2


println["dict_file: $dict_file"]
println["n: $n"]
println["lang: $lang"]

num_chars = 26
chars = map[{|i| char[i]}, 97 to 97+num_chars-1]

// for Swedish words
if lang == "swe" || lang == "Swe"
{
  num_chars = 29
  for c = ["å","ä","ö"]
     chars.push[c]
}

char_len = num_chars
println["char_len: $char_len"]

// create the patterns
patterns = []
for i = 0 to char_len-n
{
   pattern = ".*" + join[".*", sliceLength[chars, i, n]] + ".*"
   patterns.push[pattern]
}

// read the dict file
dict = split["\n", read["file:" + dict_file]]
words = []
for word = dict
{
  words.push[lc[word]]
}

println["Finished reading file."]
println["Total number of words: " + length[words]]

for pattern = patterns
{
   matched_words = new array
   println["\nTesting pattern: $pattern"]
   for word = words
      if word =~ regex[pattern]
         matched_words.push[word]
      
   println["matched_words: $matched_words"]
}
