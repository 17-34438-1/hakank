// 
// Project Euler problem 49 in Frink
//
// """  
// The arithmetic sequence, 1487, 4817, 8147, in which each of the terms 
// increases by 3330, is unusual in two ways: (i) each of the three terms are 
// prime, and, (ii) each of the 4-digit numbers are permutations of one another.
//
// There are no arithmetic sequences made up of three 1-, 2-, or 3-digit primes, 
// exhibiting this property, but there is one other 4-digit increasing sequence.
//
// What 12-digit number do you form by concatenating the three terms 
// in this sequence?
//
// """
//
// This Frink program was created by Hakan Kjellerstrand (hakank@bonetmail.com)
// Also see my Frink page: http://www.hakank.org/frink/
//

get_element[n, ll, diff] :=
{
  res = 0
  for p = ll
  {
    if p > n && p-n == diff
      res = p
  }
  
  return res

}


check_perms[n, diff] := 
{

  pp = map[{|i| parseInt[i]}, charList[""+n]]
  all_perms1 = pp.lexicographicPermute[]
  if all_perms1 == []
    return []

  // all primes > 1000
  all_perms = new array
  for p = all_perms1
    if p@0 > 0
      if isPrime[v = parseInt[join["", p]]] 
        all_perms.push[v]

  ll = []
  p1 = p2 = 0
  p1 = get_element[n, all_perms, diff]
  if p1 > 0
    p2 = get_element[p1, all_perms, diff]

  if p2 > 0
    ll = [n, p1, p2]

  return ll
}


diff = 3330
res = 0
for n = 1001 to 9999 step 2
  if isPrime[n] 
  {
    c = check_perms[n, diff]
    if c != [] and n != 1487
      res = c
  }

  
result = join["", map[{|i| ""+i}, res]]
// println["terms: $res"]
println["$result"]
