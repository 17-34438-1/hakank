// 
// Soundex in Frink
//
// See for example:
//   http://en.wikipedia.org/wiki/soundex
//   http://rosettacode.org/wiki/Soundex
// 
// This Frink program was created by Hakan Kjellerstrand (hakank@bonetmail.com)
// Also see my Frink page: http://www.hakank.org/frink/
//

//
// This program also contains: maxSoundex and deSoundex.
//

// for soundex and zip
use HakankUtils.frink


// Get the maximum length of a Soundex, i.e. the longest
// string that not ends with 0
maxSoundex[s] :=
{
   s2 = ""
   for i = 1 to length[s]-1
   {
     ss = soundex[s, i]
      if last[ss] == "0"
         return s2
     s2 = ss
   }

   return s2
}

soundexMap[] :=
{
   m = new dict
   for [c, d] = zip[charList["ABCDEFGHIJKLMNOPQRSTUVWXYZ"], charList["01230120022455012623010202"]]
   {
      if !m.containsKey[d]
         m@d = ""
      m@d = m@d + c
   }

   return m
}

//
// deSoundex[s]: unravels a Soundex string 
//
deSoundex[s] :=
{

   m = soundexMap[]
   
   res = new array
   res.push[substrLen[s,0,1]] // The first character
   for d = charList[s]
      if d =~ %r/\d/
         res.push["$d -> " + m@d]

   return res
}

// From J's test at http://rosettacode.org/wiki/Soundex
// And O'Hara from the Forth test
names = split[" ", "Lloyd Woolcock Donnell Baragwanath Williams Ashcroft Euler Ellery Gauss Ghosh Hilbert Heilbronn Knuth Kant Ladd Lukasiewicz Lissajous O'Hara"]
soundexNames = split[" ", "L300 W422 D540 B625 W452 A226 E460 E460 G200 G200 H416 H416 K530 K530 L300 L222 L222 O600"]

i = 0
all_ok = true
for n = names
{
  s = soundex[n, 4]
  t = soundexNames@i
  checkx = s == t
  check_str = ""
  check_str = "\tOK"
  if !checkx
  {
    all_ok = false
    check_str = "\tNOT OK should be " + t
  }
  println[n + ":\t" + s + " " + check_str]
  i = i + 1
}

if all_ok
  println["ALL OK!"]

println["\nThe mappings of soundex numbers to characters"]
m = new dict
for [c, d] = zip[charList["ABCDEFGHIJKLMNOPQRSTUVWXYZ"], charList["01230120022455012623010202"]]
{
   if !m.containsKey[d]
      m@d = new array   
   m@d.push[c]
}
println[m]

println["\nMax Soundex:"]
for n = names
{
   println["$n: " + maxSoundex[n] + " deSoundex: " + deSoundex[maxSoundex[n]]]
}

println["\nLongest Soundex in a wordlist"]
maxLength = 0;
maxWords = new array
lonestSoundex = ""
for word = lines["file:unixdict.txt"]
{
   s = maxSoundex[word]
   if (len=length[s]) > maxLength
   {
      maxLength = len
      maxWords = new array
      maxWords.push[word]
      longestSoundex = s
      println["maxLength: $maxLength maxWord: $maxWords longest Soundex:$longestSoundex"]
      println[deSoundex[s]]
   }
}

println["maxLength: $maxLength maxWord: $maxWords longest Soundex:$longestSoundex"]

