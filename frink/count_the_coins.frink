// 
// Count the coins in Frink
// 
// From
// http://rosettacode.org/wiki/Count_the_coins
// """
// There are four types of common coins in US currency: 
//     quarters (25 cents), dimes (10), nickels (5) and pennies (1). 
// There are 6 ways to make change for 15 cents:
//
//     A dime and a nickel;
//     A dime and 5 pennies;
//     3 nickels;
//     2 nickels and 5 pennies;
//     A nickel and 10 pennies;
//     15 pennies. 
//
// How many ways are there to make change for a dollar using these common coins? 
// (1 dollar = 100 cents).
//
// Optional:
//
// Less common are dollar coins (100 cents); very rare are half dollars (50 cents). 
// With the addition of these two coins, how many ways are there to make change for $1000? 
// (note: the answer is larger than 232). 
// """
//
// This Frink program was created by Hakan Kjellerstrand (hakank@bonetmail.com)
// Also see my Frink page: http://www.hakank.org/frink/
//

changes[amount, coins] :=
{
   ways = makeArray[[amount+1], 0]
   ways@0 = 1
   for coin = coins
      for j = coin to amount
         ways@(j) = ways@(j) + ways@(j-coin)

   return ways@(amount)
}

println["m: 100 using: [25,10,5,1]"]
t1 = now[]
println[changes[100,[25,10,5,1]]]
println["It took " + (now[]-t1->seconds) + " seconds"]

println[]
multifor [p, m] = [[[100,50,25,10,5,1], [200, 100,50,25,10,5,1]], [100, 10000, 100000, 1000000]]
{
   println["m: $m using $p"]
   t2 = now[]
   println[changes[m,p]]
   println["It took " + (now[]-t2 -> seconds) + " seconds"]
   println[]
}
