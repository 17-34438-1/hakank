// 
// Project Euler problem 42 in Frink
//
// """
// The nth term of the sequence of triangle numbers is given by, 
//     tn = 1/2*n*(n+1); 
// so the first ten triangle numbers are:
//
// 1, 3, 6, 10, 15, 21, 28, 36, 45, 55, ...
//
// By converting each letter in a word to a number corresponding to its 
// alphabetical position and adding these values we form a word value. For example, 
// the word value for SKY is 19 + 11 + 25 = 55 = t10. If the word value 
// is a triangle number then we shall call the word a triangle word.

// Using words.txt (right click and 'Save Link/Target As...'), a 16K text file 
// containing nearly two-thousand common English words, how many 
// are triangle words?
// """
//
// This Frink program was created by Hakan Kjellerstrand (hakank@bonetmail.com)
// Also see my Frink page: http://www.hakank.org/frink/
//

triangle_number[n] := (n*(n+1))/2

get_score[name] := 
{
  total = 0
  for i = 0 to length[name]-1
    total = total+(char[substrLen[name,i,1]]-64)
  return total
}

names = split[",", read["file:words.txt"] =~ %s/"//g]
t20 = toSet[map[{|i| triangle_number[i]}, 1 to 20]]
println[length[select[names, {|w, data|data.contains[get_score[w]]}, t20]]]
