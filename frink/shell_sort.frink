// 
// Shell sort in Frink
//
// See
//  * http://en.wikipedia.org/wiki/Shell_sort
//  * http://rosettacode.org/wiki/Sorting_algorithms/Shell_sort
// 
// This Frink program was created by Hakan Kjellerstrand (hakank@bonetmail.com)
// Also see my Frink page: http://www.hakank.org/frink/
//

// From Wikipedia's pseudo code 
//
// Shell sort
//
shellSort[a] :=
{
   inc = round[length[a]/2]
   while inc > 0
   {
      for i = inc to length[a]-1
      {
         temp = a@i
         j = i
         while j >= inc && a@(j-inc) > temp
         {
            a@j = a@(j-inc)
            j = j - inc;
         }
         a@j = temp
      }
      inc = round[inc/2.2]
   }
}

a = [23, 76, 99, 58, 97, 57, 35, 89, 51, 38, 95, 92, 24, 46, 31, 24, 14, 12, 57, 78]
println[a]
shellSort[a]
println[a]

time0 = now[]
a = map[{|| random[1000]}, 1 to 10000]
shellSort[a]
println["It took " + (now[]-time0 -> "seconds") + " to sort " + length[a] + " random numbers"]

// assert that the array really is sorted
for i = 1 to length[a]-1
  if a@(i-1) > a@i
    println["Not correct: " + a@(i-1) + " should be less than " + a@i]
