$
$ Simple coloring problem in Essence'.
$
$ From Pascal Van Hentenryck "The OPL Optimization Programming Language", 
$ page 7, 42.
$
$ Compare with the following models:
$ * Comet: http://www.hakank.org/comet/map.co
$
$
$ Model created by Hakan Kjellerstrand, hakank@bonetmail.com
$ See also my Essence'/Tailor page: http://www.hakank.org/minion_tailor
ESSENCE' 1.0

letting n be 6 $ number of countries
letting Belgium be 1
letting Denmark be 2
letting France be 3
letting Germany be 4
letting Netherlands be 5
letting Luxembourg be 6

find color : matrix indexed by [int(1..n)] of int(1..4)

such that

  $ some symmetry breaking
  color[Belgium] = 1,

  color[France] != color[Belgium],
  color[France] != color[Luxembourg],
  color[France] != color[Germany],
  color[Luxembourg] != color[Germany],
  color[Luxembourg] != color[Belgium],
  color[Belgium] != color[Netherlands],
  color[Belgium] != color[Germany],
  color[Germany] != color[Netherlands],
  color[Germany] != color[Denmark]



